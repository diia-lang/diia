взяти визначення мавка;

дія нативна_дія_мавки_друк(обʼєкт_нативної_дії: адреса<мавка::Обʼєкт>, В: адреса<мавка::Виконувач>, обʼєкт_я: адреса<мавка::Обʼєкт>, аргументи: адреса<Накопичувач<адреса<мавка::Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<мавка::Назва>, адреса<мавка::Обʼєкт>>>): мавка::Результат {
  якщо аргументи != пусто {
    змінна ціль па: позитивне = 0;
    поки па < аргументи.розмір {
      ціль накопичувач_ю8 = зробити_накопичувач_ю8();
      мавка::надрукувати_обʼєкт_в_накопичувач_ю8(аргументи.дані[па], В, накопичувач_ю8::адреса, 0);
      ціль ю8 = виділити_ю8_з_накопичувача_ю8(накопичувач_ю8::адреса);
      звільнити_значення_накопичувача_ю8(накопичувач_ю8);
      надрукувати_ю8(ю8);
      звільнити(ю8);
      па += 1;
    }
  }
  вернути мавка::результат_успіх(пусто);
}

дія надрукувати_помилку(шлях: Ю8, рядок: п32, стовпець: п32, повідомлення: Ю8) {
  вивести_ю8(шлях);
  вивести_ю8(ю8":");
  вивести_п64(рядок як п64);
  вивести_ю8(ю8":");
  вивести_п64(стовпець як п64);
  вивести_ю8(ю8": ");
  надрукувати_ю8(повідомлення);
}

дія надрукувати_помилку(повідомлення: Ю8) {
  надрукувати_ю8(повідомлення);
}

дія надрукувати_падіння(В: адреса<мавка::Виконувач>, значення: адреса<мавка::Обʼєкт>, місцезнаходження_падіння: адреса<мавка::МісцезнаходженняПадіння>) {
  ціль накопичувач_ю8 = зробити_накопичувач_ю8();
  мавка::надрукувати_обʼєкт_в_накопичувач_ю8(значення, В, накопичувач_ю8::адреса, 0);
  ціль ю8 = виділити_ю8_з_накопичувача_ю8(накопичувач_ю8::адреса);
  звільнити_значення_накопичувача_ю8(накопичувач_ю8);
  якщо місцезнаходження_падіння != пусто {
    надрукувати_помилку(
      мавка::отримати_шлях_до_файлу_з_місцезнаходження_падіння(місцезнаходження_падіння),
      мавка::отримати_рядок_з_місцезнаходження_падіння(місцезнаходження_падіння),
      мавка::отримати_стовпець_з_місцезнаходження_падіння(місцезнаходження_падіння),
      ю8
    );
  } інакше {
    надрукувати_помилку(ю8);
  }
  звільнити(ю8);
}

зовнішня дія стартувати_мавку(кількість_аргументів: ц32, аргументи: памʼять<Ю8>): ц32 {
  ціль В = мавка::створити_виконувач();
  ціль глобальне_середовище = мавка::отримати_глобальне_середовище(В);
  мавка::визначити_нативну_дію(В, глобальне_середовище, ю8"друк", нативна_дія_мавки_друк);
  ціль результат_взяття_модуля = мавка::взяти_модуль(В, ю8"привіт");
  якщо результат_взяття_модуля.успіх == ні {
    надрукувати_падіння(В, результат_взяття_модуля.значення, результат_взяття_модуля.місцезнаходження_падіння);
    вернути 1;
  }
  мавка::знищити_виконувач(В);
  вернути 0;
}