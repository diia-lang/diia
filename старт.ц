взяти визначення мавка/мавка_втілення;

зовнішня дія мавка_система_виділити_сиру_памʼять(розмір: позитивне): невідома_адреса;
зовнішня дія мавка_система_певиділити_сиру_памʼять(значення: невідома_адреса, новий_розмір: позитивне): невідома_адреса;
зовнішня дія мавка_система_звільнити_сиру_памʼять(значення: невідома_адреса);
зовнішня дія мавка_система_фс_отримати_назву_файла_без_розширення(шлях: памʼять<п8>, розмір_шляху: позитивне, вихід: адреса<памʼять<п8>>, вихід_розміру: адреса<позитивне>): логічне;
зовнішня дія мавка_система_фс_прочитати_файл(шлях: памʼять<п8>, розмір_шляху: позитивне, вихід: адреса<памʼять<п8>>, вихід_розміру: адреса<позитивне>): логічне;
зовнішня дія мавка_система_фс_виправити_шлях_та_зробити_абсолютним(шлях: памʼять<п8>, розмір_шляху: позитивне, вихід: адреса<памʼять<п8>>, вихід_розміру: адреса<позитивне>): логічне;
зовнішня дія мавка_система_вв_вивести_в_стандартний_вивід(значення: памʼять<п8>, розмір_значення: позитивне);
зовнішня дія мавка_система_процес_вийти(код: ц32);
зовнішня дія мавка_система_запустити_діалог(дані: невідома_адреса, обробник: (дані: невідома_адреса, значення: памʼять<п8>, розмір_значення: позитивне) -> ніщо);

дія нативна_дія_мавки_друк(предмет_нативної_дії: адреса<мавка::Предмет>, В: адреса<мавка::Виконувач>, предмет_я: адреса<мавка::Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<мавка::Предмет>>, іменовані_аргументи: адреса<мавка::ІменованіАргументи>, дані: адреса): мавка::Результат {
  якщо аргументи != пусто {
    змінна ціль па: позитивне = 0;
    поки па < кількість_аргументів {
      ціль значення_ю8 = ю8 { пусто, 0 };
      мавка::отримати_текстове_зображення_предмета_як_Ю8(аргументи[па], В, значення_ю8::адреса);
      мавка::вивести_Ю8(В, значення_ю8);
      мавка::звільнити(В, значення_ю8.дані як адреса);
      якщо (па + 1) < кількість_аргументів {
        мавка::вивести_Ю8(В, ю8" ");
      }
      па += 1;
    }
    мавка::вивести_Ю8(В, ю8"\n");
  }
  вернути мавка::результат_успіх(В, пусто);
}

дія надрукувати_помилку(В: адреса<мавка::Виконувач>, шлях: памʼять<п8>, рядок: позитивне, повідомлення: памʼять<п8>) {
  мавка::вивести_Ю8(В, ю8 { шлях, МаМа::порахувати_розмір_Ю8(шлях) });
  мавка::вивести_Ю8(В, ю8":");
  мавка::вивести_п64(В, п64(рядок));
  мавка::вивести_Ю8(В, ю8": ");
  мавка::надрукувати_Ю8(В, ю8 { повідомлення, МаМа::порахувати_розмір_Ю8(повідомлення) });
}

дія надрукувати_помилку(В: адреса<мавка::Виконувач>, шлях: ю8, рядок: позитивне, повідомлення: памʼять<п8>) {
  мавка::вивести_Ю8(В, шлях);
  мавка::вивести_Ю8(В, ю8":");
  мавка::вивести_п64(В, п64(рядок));
  мавка::вивести_Ю8(В, ю8": ");
  мавка::надрукувати_Ю8(В, ю8 { повідомлення, МаМа::порахувати_розмір_Ю8(повідомлення) });
}

дія надрукувати_помилку(В: адреса<мавка::Виконувач>, шлях: ю8, рядок: позитивне, повідомлення: ю8) {
  мавка::вивести_Ю8(В, шлях);
  мавка::вивести_Ю8(В, ю8":");
  мавка::вивести_п64(В, п64(рядок));
  мавка::вивести_Ю8(В, ю8": ");
  мавка::надрукувати_Ю8(В, повідомлення);
}

дія надрукувати_помилку(В: адреса<мавка::Виконувач>, повідомлення: памʼять<п8>) {
  мавка::надрукувати_Ю8(В, ю8 { повідомлення, МаМа::порахувати_розмір_Ю8(повідомлення) });
}

дія надрукувати_помилку(В: адреса<мавка::Виконувач>, повідомлення: ю8) {
  мавка::надрукувати_Ю8(В, повідомлення);
}

дія надрукувати_падіння(В: адреса<мавка::Виконувач>, значення: адреса<мавка::Предмет>, падіння: адреса<мавка::Падіння>) {
  ціль значення_ю8 = ю8 { пусто, 0 };
  мавка::отримати_текстове_зображення_предмета_як_Ю8(значення, В, значення_ю8::адреса);
  якщо падіння.місцезнаходження != пусто {
    надрукувати_помилку(
      В,
      мавка::отримати_шлях_до_файлу_з_місцезнаходження_падіння(В, падіння.місцезнаходження),
      мавка::отримати_рядок_з_місцезнаходження_падіння(В, падіння.місцезнаходження),
      значення_ю8
    );
  } інакше {
    надрукувати_помилку(В, значення_ю8);
  }
  мавка::звільнити_памʼять(В, значення_ю8.дані);
}

дія виділити_памʼять(розмір: позитивне): памʼять<п8> {
  вернути мавка_система_виділити_сиру_памʼять(розмір) як памʼять<п8>;
}

дія виконувач_виділити_памʼять(В: адреса<мавка::Виконувач>, розмір: позитивне): памʼять<п8> {
  вернути мавка_система_виділити_сиру_памʼять(розмір) як памʼять<п8>;
}

дія виконувач_перевиділити_памʼять(В: адреса<мавка::Виконувач>, значення: памʼять<п8>, новий_розмір: позитивне): памʼять<п8> {
  вернути мавка_система_певиділити_сиру_памʼять(значення як невідома_адреса, новий_розмір) як памʼять<п8>;
}

дія виконувач_звільнити_памʼять(В: адреса<мавка::Виконувач>, значення: памʼять<п8>) {
  мавка_система_звільнити_сиру_памʼять(значення як невідома_адреса);
}

дія виконувач_вивести(В: адреса<мавка::Виконувач>, значення: текст) {
}

дія виконувач_вивести_Ю8(В: адреса<мавка::Виконувач>, значення: ю8) {
  мавка_система_вв_вивести_в_стандартний_вивід(значення.дані, значення.розмір);
}

дія виконувач_померти(В: адреса<мавка::Виконувач>, код: ц32) {
  мавка_система_процес_вийти(код);
}

дія виконувач_прочитати_файл(В: адреса<мавка::Виконувач>, шлях: ю8, вихід: адреса<ю8>): позитивне {
  ціль прочитаний_файл = ю8 { пусто, 0 };
  якщо мавка_система_фс_прочитати_файл(шлях.дані, шлях.розмір, прочитаний_файл.дані::адреса, прочитаний_файл.розмір::адреса) == ні {
    вернути 1;
  }
  вихід::вміст = прочитаний_файл;
  вернути 0;
}

дія виконувач_виправити_шлях(В: адреса<мавка::Виконувач>, шлях: ю8, вихід: адреса<ю8>): позитивне {
  ціль виправлений_шлях = ю8 { пусто, 0 };
  якщо мавка_система_фс_виправити_шлях_та_зробити_абсолютним(шлях.дані, шлях.розмір, виправлений_шлях.дані::адреса, виправлений_шлях.розмір::адреса) == ні {
    вернути 1;
  }
  вихід::вміст = виправлений_шлях;
  вернути 0;
}

структура ДаніДіалогуМавки {
  В: адреса<мавка::Виконувач>;
  обробник: (дані: адреса, значення: памʼять<п8>, розмір_значення: позитивне) -> ніщо;
  дані: адреса;
}

дія обробник_діалогу_мавки(дані: невідома_адреса, значення: памʼять<п8>, розмір_значення: позитивне) {
  ціль дані_ділогу_мавки = дані як адреса<ДаніДіалогуМавки>;
  дані_ділогу_мавки.обробник(дані_ділогу_мавки.дані, значення, розмір_значення);
}

дія виконувач_розпочати_діалог(В: адреса<мавка::Виконувач>, дані: адреса, обробник: (дані: адреса, значення: памʼять<п8>, розмір_значення: позитивне) -> ніщо) {
  ціль дані_ділогу_мавки = мавка::виділити<ДаніДіалогуМавки>(В);
  дані_ділогу_мавки.В = В;
  дані_ділогу_мавки.обробник = обробник;
  дані_ділогу_мавки.дані = дані;
  мавка_система_запустити_діалог(дані_ділогу_мавки, обробник_діалогу_мавки);
  мавка::звільнити(В, дані_ділогу_мавки як адреса);
}

зовнішня дія стартувати_мавку(кількість_аргументів: ц32, аргументи: памʼять<памʼять<п8>>): ц32 {
  ціль В = мавка::виділити_виконувач(виділити_памʼять);
  мавка::налаштувати_виконувач(В, мавка::ВерсіяМавки { 0, 113, 0 }, мавка::Система {
    виділити_сиру_памʼять = виконувач_виділити_памʼять,
    перевиділити_сиру_памʼять = виконувач_перевиділити_памʼять,
    звільнити_сиру_памʼять = виконувач_звільнити_памʼять,
    вивести = виконувач_вивести,
    вивести_Ю8 = виконувач_вивести_Ю8,
    померти = виконувач_померти,
    виправити_шлях = виконувач_виправити_шлях,
    прочитати_файл = виконувач_прочитати_файл,
    розпочати_діалог = виконувач_розпочати_діалог,
  });
  мавка::визначити_глобальний_підмет(В, мавка::назва_з_Ю8(В, ю8"друк"), мавка::створити_нативну_дію(В, мавка::назва_з_Ю8(В, ю8"друк"), нативна_дія_мавки_друк, пусто, пусто));

  якщо кількість_аргументів == 1 {
    ціль результат = мавка::почати_діалог(В);
    вернути 0;
  } інакше {
    якщо МаМа::перевірити_чи_ю8_рівні(ю8 { аргументи[1], МаМа::порахувати_розмір_Ю8(аргументи[1]) }, ю8"допомога") == так {

    } інакше {
      ціль шлях = ю8 { аргументи[1], МаМа::порахувати_розмір_Ю8(аргументи[1]) };
      ціль назва_файлу_без_розширення = ю8 { пусто, 0 };
      якщо мавка_система_фс_отримати_назву_файла_без_розширення(шлях.дані, шлях.розмір, назва_файлу_без_розширення.дані::адреса, назва_файлу_без_розширення.розмір::адреса) == ні {
        // потім: помилка
        вернути 1;
      }
      ціль результат_взяття_модуля = мавка::взяти_файл_Ю8(В, мавка::назва_з_Ю8(В, назва_файлу_без_розширення), шлях);
      якщо результат_взяття_модуля.падіння != пусто {
        надрукувати_падіння(В, результат_взяття_модуля.значення, результат_взяття_модуля.падіння);
        вернути 1;
      }
    }
  }

  вернути 0;
}