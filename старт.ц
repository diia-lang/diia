взяти визначення мавка;

дія нативна_дія_мавки_друк(обʼєкт_нативної_дії: адреса<мавка::Обʼєкт>, В: адреса<мавка::Виконувач>, обʼєкт_я: адреса<мавка::Обʼєкт>, аргументи: адреса<мавка::Накопичувач<адреса<мавка::Обʼєкт>>>, іменовані_аргументи: адреса<мавка::Реєстр<адреса<мавка::Назва>, адреса<мавка::Обʼєкт>>>, дані: адреса): мавка::Результат {
  якщо аргументи != пусто {
    змінна ціль па: позитивне = 0;
    поки па < аргументи.розмір {
      ціль накопичувач_ю8 = МаМа::зробити_накопичувач_ю8();
      мавка::надрукувати_обʼєкт_в_накопичувач_ю8(аргументи.дані[па], В, накопичувач_ю8::адреса, 0);
      ціль ю8 = МаМа::виділити_ю8_з_накопичувача_ю8(накопичувач_ю8::адреса);
      МаМа::звільнити_значення_накопичувача_ю8(накопичувач_ю8);
      МаМа::надрукувати_Ю8(ю8);
      мавка::звільнити(ю8);
      па += 1;
    }
  }
  вернути мавка::результат_успіх(пусто);
}

дія надрукувати_помилку(шлях: мавка::Ю8, рядок: п32, повідомлення: мавка::Ю8) {
  МаМа::вивести_Ю8(шлях);
  МаМа::вивести_Ю8(ю8":");
  МаМа::вивести_п64(п64(рядок));
  МаМа::вивести_Ю8(ю8": ");
  МаМа::надрукувати_Ю8(повідомлення);
}

дія надрукувати_помилку(повідомлення: мавка::Ю8) {
  МаМа::надрукувати_Ю8(повідомлення);
}

дія надрукувати_падіння(В: адреса<мавка::Виконувач>, значення: адреса<мавка::Обʼєкт>, падіння: адреса<мавка::Падіння>) {
  ціль накопичувач_ю8 = МаМа::зробити_накопичувач_ю8();
  мавка::надрукувати_обʼєкт_в_накопичувач_ю8(значення, В, накопичувач_ю8::адреса, 0);
  ціль ю8 = МаМа::виділити_ю8_з_накопичувача_ю8(накопичувач_ю8::адреса);
  МаМа::звільнити_значення_накопичувача_ю8(накопичувач_ю8);
  якщо падіння.місцезнаходження != пусто {
    надрукувати_помилку(
      мавка::отримати_шлях_до_файлу_з_місцезнаходження_падіння(падіння.місцезнаходження),
      мавка::отримати_рядок_з_місцезнаходження_падіння(падіння.місцезнаходження),
      ю8
    );
  } інакше {
    надрукувати_помилку(ю8);
  }
  мавка::звільнити(ю8);
}

зовнішня дія перевірити_чи_ю8_закінчується_на(значення: мавка::Ю8, закінчення: мавка::Ю8): п8;
зовнішня дія отримати_назву_файлу_без_розширення(шлях: мавка::Ю8, вихід: адреса<мавка::Ю8>);

зовнішня дія стартувати_мавку(кількість_аргументів: ц32, аргументи: адреса<адреса<п8>>): ц32 {
  ціль В = мавка::створити_виконувач();
  мавка::визначити_глобальний_субʼєкт(В, мавка::назва_з_Ю8(В, ю8"друк"), мавка::створити_нативну_дію(В, мавка::назва_з_Ю8(В, ю8"друк"), нативна_дія_мавки_друк, пусто, пусто));

  якщо кількість_аргументів == 1 {
    ціль результат = мавка::почати_діалог(В);
    вернути 0;
  } інакше {
    якщо МаМа::порівняти_Ю8(аргументи[1], ю8"допомога") == 0 {

    } інакше {
      ціль шлях = аргументи[1];
      якщо перевірити_чи_ю8_закінчується_на(шлях, ю8".м") == 0 {
        // потім: помилка
        вернути 1;
      }
      ціль назва_файлу_без_розширення: мавка::Ю8 = пусто;
      отримати_назву_файлу_без_розширення(шлях, назва_файлу_без_розширення::адреса);
      ціль результат_взяття_модуля = мавка::взяти_файл_Ю8(В, мавка::назва_з_Ю8(В, назва_файлу_без_розширення), шлях);
      якщо результат_взяття_модуля.падіння != пусто {
        надрукувати_падіння(В, результат_взяття_модуля.значення, результат_взяття_модуля.падіння);
        вернути 1;
      }
    }
  }

  вернути 0;
}