взяти визначення МаМа/допоміжне/біб;

структура ЕлементКарти<К, З>;
структура Карта<К, З>;

структура ЕлементКарти<К, З> {
  ключ: К;
  значення: З;
}

структура Карта<К, З> {
  розмір: позитивне;
  елементи: памʼять<ЕлементКарти<К, З>>;
  дія_перевірки_ключа: (а: К, б: К) -> логічне;
}

дія зробити_карту<К, З>(дія_перевірки_ключа: (а: К, б: К) -> логічне): Карта<К, З> {
  ціль карта = Карта<К, З> {};
  карта.розмір = 0;
  карта.елементи = пусто;
  карта.дія_перевірки_ключа = дія_перевірки_ключа;
  вернути карта;
}

дія отримати_з_карти<К, З>(карта: адреса<Карта<К, З>>, ключ: К): адреса<ЕлементКарти<К, З>> {
  змінна ціль позиція: позитивне = 0;
  поки позиція < карта.розмір {
    якщо карта.дія_перевірки_ключа(карта.елементи[позиція].ключ, ключ) {
      вернути карта.елементи[позиція]::адреса;
    }
    позиція = позиція + 1;
  }
  вернути пусто;
}

дія покласти_у_карту<К, З>(карта: адреса<Карта<К, З>>, ключ: К, значення: З) {
  змінна ціль елемент_карти = отримати_з_карти(карта, ключ);
  якщо елемент_карти == пусто {
    карта.елементи = перевиділити<ЕлементКарти<К, З>>(карта.елементи, (карта.розмір + 1) як ц32);
    карта.елементи[карта.розмір] = ЕлементКарти<К, З> { ключ = ключ, значення = значення };
    карта.розмір = карта.розмір + 1;
  } інакше {
    елемент_карти.значення = значення;
  }
}

дія звільнити_карту<К, З>(карта: адреса<Карта<К, З>>) {
  звільнити(карта.елементи);
  звільнити(карта);
}

дія розширити_карту<К, З>(карта: адреса<Карта<К, З>>, новий_розмір: позитивне) {
  якщо новий_розмір > карта.розмір {
    карта.елементи = перевиділити<ЕлементКарти<К, З>>(карта.елементи, новий_розмір як ц32);
    карта.розмір = новий_розмір;
  }
}