взяти визначення ./біб;

секція МаМа {
  місцева дія перевірити_чи_ю8_рівні(а: Ю8, б: Ю8): логічне {
    змінна ціль позиція: позитивне = 0;
    поки а[позиція] == б[позиція] {
      якщо а[позиція] == 0 {
        вернути так;
      }
      позиція = позиція + 1;
    }
    вернути ні;
  }

  місцева дія виділити_копію_байтів(вхід: памʼять<п8>, розмір_входу: позитивне, вихід: адреса<памʼять<п8>>): позитивне {
    якщо розмір_входу == 0 {
      вихід[0] = пусто як памʼять<п8>;
      вернути 0;
    }
    ціль копія = виділити<п8>(ц32(розмір_входу));
    змінна ціль позиція: позитивне = 0;
    поки вхід[позиція] != 0 {
      копія[позиція] = вхід[позиція];
      позиція += 1;
    }
    вихід[0] = копія;
    вернути позиція;
  }

  місцева дія виділити_копію_КД(вхід: памʼять<п8>, розмір_входу: позитивне, вихід: адреса<памʼять<п8>>): позитивне {
    ціль копія = виділити<п8>(ц32(розмір_входу) + 1);
    змінна ціль позиція: позитивне = 0;
    поки вхід[позиція] != 0 {
      копія[позиція] = вхід[позиція];
      позиція += 1;
    }
    копія[позиція] = 0;
    вихід[0] = копія;
    вернути позиція;
  }

  місцева дія виділити_копію_Ю8(вхід: Ю8, розмір_входу: позитивне, вихід: адреса<Ю8>): позитивне {
    ціль копія = виділити<п8>(ц32(розмір_входу) + 1);
    змінна ціль позиція: позитивне = 0;
    поки вхід[позиція] != 0 {
      копія[позиція] = вхід[позиція];
      позиція += 1;
    }
    копія[позиція] = 0;
    вихід[0] = копія;
    вернути позиція;
  }

  місцева дія виділити_копію_Ю32(вхід: Ю32, розмір_входу: позитивне, вихід: адреса<Ю32>): позитивне {
    ціль копія = виділити<п32>(ц32(розмір_входу) + 1);
    змінна ціль позиція: позитивне = 0;
    поки вхід[позиція] != 0 {
      копія[позиція] = вхід[позиція];
      позиція += 1;
    }
    копія[позиція] = 0;
    вихід[0] = копія;
    вернути позиція;
  }

  місцева дія дізнатись_розмір_символа_юнікоду(перший_байт: п8): п8 {
    якщо (перший_байт & 128) == 0 {
      вернути 1;
    }
    якщо (перший_байт & 224) == 192 {
      вернути 2;
    }
    якщо (перший_байт & 240) == 224 {
      вернути 3;
    }
    якщо (перший_байт & 248) == 240 {
      вернути 4;
    }
    вернути 0;
  }

  місцева дія перекодувати_Ю8_в_Ю32(вхід: Ю8, розмір_входу: позитивне, адреса_виходу: адреса<Ю32>): позитивне {
    змінна ціль вихід: Ю32 = виділити<п32>(ц32(розмір_входу) + 1);
    змінна ціль розмір_виходу: позитивне = 0;
    змінна ціль х: позитивне = 0;
    поки х < розмір_входу {
      ціль перший_байт = вхід[х];
      ціль розмір_символа = дізнатись_розмір_символа_юнікоду(перший_байт);
      якщо розмір_символа == 1 {
        вихід[розмір_виходу] = п32(перший_байт & 127);
        розмір_виходу = розмір_виходу + 1;
      }
      якщо розмір_символа == 2 {
        ціль другий_байт = вхід[х + 1];
        вихід[розмір_виходу] = (п32(перший_байт & 31) << 6) | п32(другий_байт & 63);
        розмір_виходу = розмір_виходу + 1;
        х = х + 1;
      }
      якщо розмір_символа == 3 {
        ціль другий_байт = вхід[х + 1];
        ціль третій_байт = вхід[х + 2];
        вихід[розмір_виходу] = (п32(перший_байт & 15) << 12) | (п32(другий_байт & 63) << 6) | п32(третій_байт & 63);
        розмір_виходу = розмір_виходу + 1;
        х = х + 2;
      }
      якщо розмір_символа == 4 {
        ціль другий_байт = вхід[х + 1];
        ціль третій_байт = вхід[х + 2];
        ціль четвертий_байт = вхід[х + 3];
        вихід[розмір_виходу] = (п32(перший_байт & 15) << 18) | (п32(другий_байт & 63) << 12) | (п32(третій_байт & 63) << 6) | п32(четвертий_байт & 63);
        розмір_виходу = розмір_виходу + 1;
        х = х + 3;
      }
      х = х + 1;
    }
    вихід[розмір_виходу] = 0;
    вихід = перевиділити<п32>(вихід, ц32(розмір_виходу) + 1);
    адреса_виходу[0] = вихід;
    вернути розмір_виходу;
  }

  місцева дія перекодувати_Ю32_в_Ю8(вхід: Ю32, розмір_входу: позитивне, адреса_виходу: адреса<Ю8>): позитивне {
    адреса_виходу[0] = mama_convert_utf32_to_utf8(вхід);
    вернути порахувати_розмір_Ю8(адреса_виходу[0]);
  }

  місцева дія порівняти_КД(а: памʼять<п8>, б: памʼять<п8>): ціле {
    змінна ціль позиція: позитивне = 0;
    поки а[позиція] != 0 {
      якщо б[позиція] == 0 {
        вернути ціле(а[позиція] - б[позиція]);
      }
      якщо а[позиція] != б[позиція] {
        вернути ціле(а[позиція] - б[позиція]);
      }
      позиція += 1;
    }
    вернути ціле(а[позиція] - б[позиція]);
  }

  місцева дія порівняти_Ю8(а: Ю8, б: Ю8): ціле {
    змінна ціль позиція: позитивне = 0;
    поки а[позиція] != 0 {
      якщо б[позиція] == 0 {
        вернути ціле(а[позиція] - б[позиція]);
      }
      якщо а[позиція] != б[позиція] {
        вернути ціле(а[позиція] - б[позиція]);
      }
      позиція += 1;
    }
    вернути ціле(а[позиція] - б[позиція]);
  }

  місцева дія порівняти_Ю32(а: Ю32, б: Ю32): ціле {
    змінна ціль позиція: позитивне = 0;
    поки а[позиція] != 0 {
      якщо б[позиція] == 0 {
        вернути ціле(а[позиція] - б[позиція]);
      }
      якщо а[позиція] != б[позиція] {
        вернути ціле(а[позиція] - б[позиція]);
      }
      позиція += 1;
    }
    вернути ціле(а[позиція] - б[позиція]);
  }

  місцева дія вивести_Ю8(значення: Ю8) {
    mama_print_utf8(значення);
  }

  місцева дія надрукувати_Ю8(значення: Ю8) {
    mama_println_utf8(значення);
  }

  місцева дія перетворити_д64_в_Ю8(значення: д64, вихід: адреса<Ю8>): позитивне {
    вернути mama_double_to_string(значення, вихід);
  }

  місцева дія перетворити_п64_в_ю8(значення: п64): Ю8 {
    змінна ціль буфер = виділити<п8>(21);
    змінна ціль індекс: ц32 = 20;
    буфер[20] = 0;
    поки (значення > 0) {
      індекс = індекс - 1;
      буфер[індекс] = п8(значення % 10) + 48;
      значення = значення / 10;
    }
    якщо (індекс == 20) {
      індекс = індекс - 1;
      буфер[індекс] = 48;
    }
    ціль результуючий_буфер = виділити<п8>(21ц32 - індекс);
    змінна ціль х: ц32 = 0;
    поки (індекс < 21) {
      результуючий_буфер[х] = буфер[індекс];
      х = х + 1;
      індекс = індекс + 1;
    }
    звільнити(буфер);
    вернути результуючий_буфер;
  }

  місцева дія вивести_п64(значення: п64) {
    ціль п64ю8 = перетворити_п64_в_ю8(значення);
    вивести_Ю8(п64ю8);
    звільнити(п64ю8);
  }

  місцева дія надрукувати_п64(значення: п64) {
    ціль п64ю8 = перетворити_п64_в_ю8(значення);
    надрукувати_Ю8(п64ю8);
    звільнити(п64ю8);
  }

  місцева дія вивести_д64(значення: д64) {
    ціль д64ю8: Ю8 = пусто;
    якщо перетворити_д64_в_Ю8(значення, д64ю8::адреса) == 0 {
      ПОМЕРТИ(ю8"Помилка при конвертації дійсного числа в рядок");
    }
    вивести_Ю8(д64ю8);
    звільнити(д64ю8);
  }

  місцева дія надрукувати_д64(значення: д64) {
    ціль д64ю8: Ю8 = пусто;
    якщо перетворити_д64_в_Ю8(значення, д64ю8::адреса) == 0 {
      ПОМЕРТИ(ю8"Помилка при конвертації дійсного числа в рядок");
    }
    надрукувати_Ю8(д64ю8);
    звільнити(д64ю8);
  }

  місцева дія порахувати_розмір_КД(значення: адреса<п8>): позитивне {
    змінна ціль довжина: позитивне = 0;
    поки значення[довжина] != 0 {
      довжина += 1;
    }
    вернути довжина;
  }

  місцева дія порахувати_розмір_Ю8(значення: адреса<п8>): позитивне {
    змінна ціль довжина: позитивне = 0;
    поки значення[довжина] != 0 {
      довжина += 1;
    }
    вернути довжина;
  }

  місцева дія порахувати_розмір_Ю32(значення: адреса<п32>): позитивне {
    змінна ціль довжина: позитивне = 0;
    поки значення[довжина] != 0 {
      довжина += 1;
    }
    вернути довжина;
  }
}