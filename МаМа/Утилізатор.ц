взяти визначення МаМа;

секція МаМа {
  місцева дія зробити_утилізатор(): Утилізатор {
    ціль утилізатор = Утилізатор {};
    утилізатор.початок_обʼєктів = пусто;
    утилізатор.кількість_обʼєктів = 0;
    утилізатор.кількість_обʼєктів_щоб_запустити_утилізацію = 0;
    утилізатор.кількість_середовищ_щоб_запустити_утилізацію = 0;
    утилізатор.початок_середовищ = пусто;
    утилізатор.кількість_середовищ = 0;
    вернути утилізатор;
  }

  місцева дія позначити(М: адреса<Машина>) {
    змінна ціль п: позитивне = 0;
    поки п < М.накопичувач_виконання.розмір {
      позначити_обʼєкт(М.накопичувач_виконання.дані[п]);
      п = п + 1;
    }
    п = 0;
    поки п < М.накопичувач_реєстрів_аргументів.розмір {
      змінна ціль р: позитивне = 0;
      поки р < М.накопичувач_реєстрів_аргументів.дані[п].розмір {
        позначити_обʼєкт(М.накопичувач_реєстрів_аргументів.дані[п].елементи[р].значення);
        р = р + 1;
      }
      п = п + 1;
    }
    п = 0;
    поки п < М.накопичувач_реєстрів_параметрів.розмір {
      змінна ціль р: позитивне = 0;
      поки р < М.накопичувач_реєстрів_параметрів.дані[п].розмір {
        змінна ціль пр: позитивне = 0;
        поки пр < М.накопичувач_реєстрів_параметрів.дані[п].елементи[р].значення.типи.розмір {
          позначити_обʼєкт(М.накопичувач_реєстрів_параметрів.дані[п].елементи[р].значення.типи.дані[пр]);
          пр = пр + 1;
        }
        р = р + 1;
      }
      п = п + 1;
    }
    позначити_середовище(М.глобальне_середовище);
    позначити_обʼєкт(М.обʼєкт_структури_обʼєкт);
    позначити_обʼєкт(М.обʼєкт_структури_Структура);
    позначити_обʼєкт(М.обʼєкт_структури_число);
    позначити_обʼєкт(М.обʼєкт_структури_текст);
    позначити_обʼєкт(М.обʼєкт_структури_Дія);
    позначити_обʼєкт(М.обʼєкт_структури_НативнаДія);
  }

  місцева дія утилізувати(М: адреса<Машина>) {
    позначити(М);
    змінна ціль попередній_обʼєкт: адреса<Обʼєкт> = пусто;
    змінна ціль обʼєкт = М.утилізатор.початок_обʼєктів;
    поки обʼєкт != пусто {
      ціль наступний_обʼєкт = обʼєкт.наступний_обʼєкт;
      якщо обʼєкт.позначено == ні {
        якщо попередній_обʼєкт == пусто {
          М.утилізатор.початок_обʼєктів = наступний_обʼєкт;
        } інакше {
          попередній_обʼєкт.наступний_обʼєкт = наступний_обʼєкт;
        }
        звільнити_обʼєкт(обʼєкт);
        М.утилізатор.кількість_обʼєктів = М.утилізатор.кількість_обʼєктів - 1;
      } інакше {
        попередній_обʼєкт = обʼєкт;
      }
      обʼєкт = наступний_обʼєкт;
    }
  }

  місцева дія позначити_обʼєкт(обʼєкт: адреса<Обʼєкт>) {
    якщо обʼєкт == пусто {
      вернути;
    }
    якщо обʼєкт.позначено == так {
      вернути;
    }
    обʼєкт.позначено = так;
  }

  місцева дія позначити_середовище(середовище: адреса<Середовище>) {
    // ...
  }
}