взяти визначення МаМа;

секція МаМа {
  місцева дія виділити_обʼєкт(М: адреса<Машина>, обʼєкт_структури: адреса<Обʼєкт>): адреса<Обʼєкт> {
    ціль обʼєкт = виділити<Обʼєкт>();
    обʼєкт.обʼєкт_структури = пусто;
    обʼєкт.властивості = зробити_карту<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    вернути обʼєкт;
  }

  місцева дія виділити_обʼєкт_структури(М: адреса<Машина>, предок: адреса<Обʼєкт>): адреса<Обʼєкт> {
    ціль обʼєкт_структури = виділити_обʼєкт(М, М.обʼєкт_структури_структура);
    ціль дані_обʼєкта_структури = ДаніОбʼєктаСтруктури {};
    якщо предок == пусто {
      дані_обʼєкта_структури.предок = М.обʼєкт_структури_обʼєкт;
    } інакше {
      дані_обʼєкта_структури.предок = предок;
    }
    обʼєкт_структури.дані = дані_обʼєкта_структури;
    вернути обʼєкт_структури;
  }

  місцева дія виділити_обʼєкт_числа(М: адреса<Машина>, значення: д64): адреса<Обʼєкт> {
    ціль обʼєкт_числа = виділити_обʼєкт(М, М.обʼєкт_структури_число);
    обʼєкт_числа.дані = значення;
    вернути обʼєкт_числа;
  }

  місцева дія виділити_машину(): адреса<Машина> {
    ціль М = виділити<Машина>();
    М.БН = зробити_базу_назв();
    М.стопка_виконання = зробити_стопку<адреса<Обʼєкт>>();
    М.стопка_накопичувачів_аргументів = зробити_стопку<адреса<НакопичувачАргументів>>();
    М.глобальне_середовище = виділити_середовище(пусто);
    М.обʼєкт_структури_обʼєкт = виділити_обʼєкт(М, пусто);
    М.обʼєкт_структури_обʼєкт.дані = ДаніОбʼєктаСтруктури {
      предок = пусто
    };
    М.обʼєкт_структури_структура = виділити_обʼєкт(М, пусто);
    М.обʼєкт_структури_структура.дані = ДаніОбʼєктаСтруктури {
      предок = М.обʼєкт_структури_обʼєкт
    };
    М.обʼєкт_структури_обʼєкт.обʼєкт_структури = М.обʼєкт_структури_структура;
    М.обʼєкт_структури_число = виділити_обʼєкт_структури(М, пусто);
    вернути М;
  }

  місцева дія виконати_код(М: адреса<Машина>, код: адреса<Код>) {
    ціль середовище = М.глобальне_середовище;
    ціль позиція_вказівки: п32 = 0;
    поки позиція_вказівки < код.розмір_вказівок {
      ціль вказівка = код.вказівки[позиція_вказівки];
      якщо вказівка == ВВизначити {
        ціль значення = забрати_з_стопки<адреса<Обʼєкт>>(М.стопка_виконання::адреса);
        ціль позиція_назви = прочитати_аргумент_вказівки_п32(код, позиція_вказівки);
        позиція_вказівки = позиція_вказівки + 4;
        ціль назва = код.назви[позиція_назви];
        змінити_в_середовищі(середовище, назва, значення);
      } інакше якщо вказівка == ВЗвернутись {
        ціль позиція_назви = прочитати_аргумент_вказівки_п32(код, позиція_вказівки);
        позиція_вказівки = позиція_вказівки + 4;
        ціль назва = код.назви[позиція_назви];
        покласти_на_стопку<адреса<Обʼєкт>>(М.стопка_виконання::адреса, знайти_в_середовищі(середовище, назва));
      } інакше якщо вказівка == ВПокластиЧисло {
        ціль позиція_числа = прочитати_аргумент_вказівки_п32(код, позиція_вказівки);
        позиція_вказівки = позиція_вказівки + 4;
        покласти_на_стопку<адреса<Обʼєкт>>(М.стопка_виконання::адреса, виділити_обʼєкт_числа(М, код.числа[позиція_числа]));
      } інакше якщо вказівка == ВДодати {
        ціль обʼєкт_числа2 = забрати_з_стопки<адреса<Обʼєкт>>(М.стопка_виконання::адреса);
        ціль обʼєкт_числа1 = забрати_з_стопки<адреса<Обʼєкт>>(М.стопка_виконання::адреса);
        якщо обʼєкт_числа1 == пусто {
          надрукувати_ю8(ю8"Немає першого числа");
          вернути;
        }
        якщо обʼєкт_числа2 == пусто {
          надрукувати_ю8(ю8"Немає другого числа");
          вернути;
        }
        ціль число1 = обʼєкт_числа1.дані як ДаніОбʼєктаЧисла;
        ціль число2 = обʼєкт_числа2.дані як ДаніОбʼєктаЧисла;
        покласти_на_стопку<адреса<Обʼєкт>>(М.стопка_виконання::адреса, виділити_обʼєкт_числа(М, число1 + число2));
      } інакше {
        вивести_ю8(ю8"Невідома вказівка на позиції ");
        вивести_п64(позиція_вказівки як п64);
        вивести_ю8(ю8": ");
        надрукувати_п64(вказівка як п64);
        вернути;
      }
      позиція_вказівки = позиція_вказівки + 1;
    }
  }

  місцева дія зробити_базу_назв(): БазаНазв {
    ціль БН = БазаНазв {};
    БН._дані = зробити_вектор<адреса<Назва>>();
    БН.предок = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"предок");
    вернути БН;
  }

  місцева дія знайти_або_виділити_й_додати_назву(БН: адреса<БазаНазв>, значення: Ю8): адреса<Назва> {
    змінна ціль позиція: позитивне = 0;
    поки позиція < БН._дані.довжина {
      ціль назва = БН._дані.дані[позиція];
      якщо перевірити_чи_ю8_рівні(назва.значення, значення) {
        вернути назва;
      }
      позиція = позиція + 1;
    }
    ціль назва = виділити_назву(значення);
    додати_до_вектору<адреса<Назва>>(БН._дані::адреса, назва);
    вернути назва;
  }

  місцева дія виділити_середовище(батьківське: адреса<Середовище>): адреса<Середовище> {
    ціль середовище = виділити<Середовище>();
    середовище.батьківське = батьківське;
    середовище.субʼєкти = зробити_карту<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    вернути середовище;
  }

  місцева дія змінити_в_середовищі(середовище: адреса<Середовище>, назва: адреса<Назва>, обʼєкт: адреса<Обʼєкт>) {
    змінити_в_карті<адреса<Назва>, адреса<Обʼєкт>>(середовище.субʼєкти::адреса, назва, обʼєкт);
  }

  місцева дія знайти_в_середовищі(середовище: адреса<Середовище>, назва: адреса<Назва>): адреса<Обʼєкт> {
    змінна ціль поточне_середовище = середовище;
    поки поточне_середовище != пусто {
      ціль знайдений_елемент_карти = отримати_з_карти<адреса<Назва>, адреса<Обʼєкт>>(поточне_середовище.субʼєкти::адреса, назва);
      якщо знайдений_елемент_карти != пусто {
        вернути знайдений_елемент_карти.значення;
      }
      поточне_середовище = поточне_середовище.батьківське;
    }
    вернути пусто;
  }
}