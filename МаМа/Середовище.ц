взяти визначення МаМа;

секція МаМа {
  місцева дія виділити_середовище(М: адреса<Машина>, батьківське: адреса<Середовище>): адреса<Середовище> {
//    надрукувати_Ю8(М, ю8"[МАВКА] виділяємо середовище");
    ціль середовище = виділити<Середовище>(М);
    середовище.наступне_середовище = М.утилізатор.початок_середовищ;
    середовище.позначено = позитивне(ні);
    середовище.батьківське = батьківське;
    середовище.субʼєкти = зробити_реєстр<адреса<Назва>, адреса<Обʼєкт>>(М, перевірити_чи_назви_рівні);
    середовище.обʼєкт_модуля = пусто;
    середовище.обʼєкт_я = пусто;
    М.утилізатор.початок_середовищ = середовище;
    М.утилізатор.кількість_середовищ = М.утилізатор.кількість_середовищ + 1;
    вернути середовище;
  }

  місцева дія змінити_в_середовищі(М: адреса<Машина>, середовище: адреса<Середовище>, назва: адреса<Назва>, обʼєкт: адреса<Обʼєкт>) {
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(М, середовище.субʼєкти::адреса, назва, обʼєкт);
  }

  місцева дія видалити_з_середовища(М: адреса<Машина>, середовище: адреса<Середовище>, назва: адреса<Назва>): логічне {
    вернути видалити_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(М, середовище.субʼєкти::адреса, назва);
  }

  місцева дія знайти_в_середовищі(М: адреса<Машина>, середовище: адреса<Середовище>, назва: адреса<Назва>): адреса<Обʼєкт> {
    змінна ціль поточне_середовище = середовище;
    поки поточне_середовище != пусто {
      ціль знайдений_елемент_карти = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(М, поточне_середовище.субʼєкти::адреса, назва);
      якщо знайдений_елемент_карти != пусто {
        вернути знайдений_елемент_карти.значення;
      }
      поточне_середовище = поточне_середовище.батьківське;
    }
    вернути пусто;
  }

  місцева дія знайти_елемент_в_середовищі(М: адреса<Машина>, середовище: адреса<Середовище>, назва: адреса<Назва>): адреса<ЕлементРеєстру<адреса<Назва>, адреса<Обʼєкт>>> {
    змінна ціль поточне_середовище = середовище;
    поки поточне_середовище != пусто {
      ціль знайдений_елемент_карти = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(М, поточне_середовище.субʼєкти::адреса, назва);
      якщо знайдений_елемент_карти != пусто {
        вернути знайдений_елемент_карти;
      }
      поточне_середовище = поточне_середовище.батьківське;
    }
    вернути пусто;
  }

  місцева дія звільнити_середовище(М: адреса<Машина>, середовище: адреса<Середовище>) {
//    надрукувати_Ю8(М, ю8"[МАВКА] звільняємо середовище");
    звільнити_значення_реєстру<адреса<Назва>, адреса<Обʼєкт>>(М, середовище.субʼєкти);
    звільнити(М, середовище);
  }
}