взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт_числа(М: адреса<Машина>, обʼєкт: адреса<Обʼєкт>) {
    надрукувати_д64(обʼєкт.дані як д64);
  }

  дія нативна_дія_чародія_додати(обʼєкт_чародії: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Вектор<адреса<Обʼєкт>>>, аргументи_з_назвами: адреса_або_пусто<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса_або_стан_помилки<Обʼєкт> {
    змінна ціль аргумент: адреса<Обʼєкт> = пусто;
    якщо аргументи.довжина != 0 {
      аргумент = аргументи.дані[0];
    } інакше якщо аргументи_з_назвами != пусто {
      // ...
    }
    якщо аргумент == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати число і пусто");
    }
    якщо аргумент.обʼєкт_структури != М.обʼєкт_структури_число {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати число і не число");
    }
    якщо обʼєкт_я == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати пусто і число");
    }
    вернути виділити_обʼєкт_числа(М, (обʼєкт_я.дані як д64) + (аргумент.дані як д64));
  }

  місцева дія виділити_обʼєкт_числа(М: адреса<Машина>, значення: д64): адреса<Обʼєкт> {
    ціль обʼєкт_числа = виділити_обʼєкт(М, М.обʼєкт_структури_число);
    обʼєкт_числа.дані = значення;
    вернути обʼєкт_числа;
  }

  місцева дія виділити_обʼєкт_структури_число(М: адреса<Машина>): адреса<Обʼєкт> {
    ціль обʼєкт_структури = виділити_обʼєкт_структури(М, ю8"число", М.обʼєкт_структури_обʼєкт);
    синонім дані_структури = обʼєкт_структури.дані як ДаніОбʼєктаСтруктури;
    дані_структури.метод_чародія_додати = виділити_обʼєкт_нативної_дії(М, нативна_дія_чародія_додати);
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(дані_структури.методи::адреса, М.БН.чародія_додати, дані_структури.метод_чародія_додати);
    вернути обʼєкт_структури;
  }
}