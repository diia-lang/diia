взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт_дії_в_буфер_ю8(обʼєкт_дії: адреса<ОбʼєктДії>, М: адреса<Машина>, буфер_ю8: адреса<БуферЮ8>, глибина: позитивне) {
    додати_до_буфера_ю8(буфер_ю8, ю8"<дія");
    якщо обʼєкт_дії.назва != пусто {
      додати_до_буфера_ю8(буфер_ю8, ю8" ");
      додати_до_буфера_ю8(буфер_ю8, обʼєкт_дії.назва.значення);
    }
    додати_до_буфера_ю8(буфер_ю8, ю8">");
  }

  місцева дія створити_дію(М: адреса<Машина>, назва: адреса<Назва>, параметри: адреса<Реєстр<адреса<Назва>, Параметр>>, середовище_створення: адреса<Середовище>, код: адреса<Код>): адреса<ОбʼєктДії> {
    ціль обʼєкт_дії = виділити_обʼєкт<ОбʼєктДії>(М, М.обʼєкт_структури_Дія);

    обʼєкт_дії.властивості = зробити_реєстр<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    обʼєкт_дії.назва = назва;
    обʼєкт_дії.параметри = параметри;
    обʼєкт_дії.середовище_створення = середовище_створення;
    обʼєкт_дії.код = код;

    вернути обʼєкт_дії;
  }

  місцева дія обʼєкт_дії_виконати(обʼєкт_дії: адреса<ОбʼєктДії>, М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса_або_стан_помилки<Обʼєкт> {
    ціль середовище_виконання_дії = виділити_середовище(М, обʼєкт_дії.середовище_створення);
    покласти_в_накопичувач<ЕлементІсторіїВиконання>(М.накопичувач_історії_виконання::адреса, ЕлементІсторіїВиконання {
      обʼєкт_дії = обʼєкт_дії,
      середовище = середовище_виконання_дії,
    });
    змінна ціль пп: позитивне = 0;
    поки пп < обʼєкт_дії.параметри.розмір {
      ціль параметр = обʼєкт_дії.параметри.елементи[пп].значення;
      змінна ціль аргумент: адреса<Обʼєкт> = пусто;
      // потім: перевірити чи аргументи не пусто
      якщо аргументи.розмір > пп {
        аргумент = аргументи.дані[пп];
      } інакше {
        якщо іменовані_аргументи == пусто {
          якщо параметр.код_значення_за_замовчуванням != пусто {
            ціль результат_виконання_значення_за_замовчуванням = виконати_код(М, середовище_виконання_дії, параметр.код_значення_за_замовчуванням);
            якщо М.стан_помилки {
              вернути результат_виконання_значення_за_замовчуванням;
            }
            ціль значення_за_замовчуванням = забрати_з_накопичувача<адреса<Обʼєкт>>(М.накопичувач_виконання::адреса);
            аргумент = значення_за_замовчуванням;
          }
        } інакше {
          ціль елемент_аргумента_з_назвою = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(іменовані_аргументи, параметр.назва);
          якщо елемент_аргумента_з_назвою == пусто {
            якщо параметр.код_значення_за_замовчуванням != пусто {
              ціль результат_виконання_значення_за_замовчуванням = виконати_код(М, середовище_виконання_дії, параметр.код_значення_за_замовчуванням);
              якщо М.стан_помилки {
                вернути результат_виконання_значення_за_замовчуванням;
              }
              ціль значення_за_замовчуванням = забрати_з_накопичувача<адреса<Обʼєкт>>(М.накопичувач_виконання::адреса);
              аргумент = значення_за_замовчуванням;
            }
          } інакше {
            аргумент = елемент_аргумента_з_назвою.значення;
          }
        }
      }
      якщо параметр.типи.розмір > 0 {
        змінна ціль пп: позитивне = 0;
        змінна ціль знайдено_тип: логічне = ні;
        поки пп < параметр.типи.розмір {
          якщо перевірити_чи_обʼєкт_є(аргумент, М, параметр.типи.дані[пп]) {
            знайдено_тип = так;
          }
          пп += 1;
        }
        якщо знайдено_тип == ні {
          увімкнути_стан_помилки(М);
          якщо аргумент == пусто {
            вернути виділити_обʼєкт_тексту_помилки_невірний_тип_аргумента(М, параметр.назва.значення, параметр.типи, пусто);
          } інакше {
            вернути виділити_обʼєкт_тексту_помилки_невірний_тип_аргумента(М, параметр.назва.значення, параметр.типи, аргумент.тип);
          }
        }
      }
      змінити_в_середовищі(середовище_виконання_дії, параметр.назва, аргумент);
      пп += 1;
    }
    ціль результат_виконання = виконати_код(М, середовище_виконання_дії, обʼєкт_дії.код);
    якщо М.стан_помилки {
      вернути результат_виконання;
    }
    забрати_з_накопичувача<ЕлементІсторіїВиконання>(М.накопичувач_історії_виконання::адреса);
    вернути результат_виконання;
  }

  місцева дія звільнити_обʼєкт_дії(обʼєкт_дії: адреса<ОбʼєктДії>, М: адреса<Машина>) {
    звільнити_значення_реєстру<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_дії.властивості);
    звільнити_реєстр<адреса<Назва>, Параметр>(обʼєкт_дії.параметри);
    звільнити(обʼєкт_дії);
  }
}