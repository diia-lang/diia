взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт_числа_в_буфер_ю8(обʼєкт_числа: адреса<ОбʼєктЧисла>, М: адреса<Машина>, буфер_ю8: адреса<БуферЮ8>, глибина: позитивне) {
    ціль д64ю8 = перетворити_д64_в_ю8(обʼєкт_числа.значення);
    додати_до_буфера_ю8(буфер_ю8, д64ю8);
    звільнити(д64ю8);
  }

  дія нативна_дія_числа_додати(обʼєкт_нативної_дії: адреса<МаМа::ОбʼєктНативноїДії>, М: адреса<МаМа::Машина>, обʼєкт_я: адреса<МаМа::Обʼєкт>, аргументи: адреса<Накопичувач<адреса<МаМа::Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<МаМа::Назва>, адреса<МаМа::Обʼєкт>>>): адреса<МаМа::Обʼєкт> {
    якщо обʼєкт_я == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо додати пусто");
    }
    якщо обʼєкт_я.тип != М.обʼєкт_структури_число {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо додати");
    }
    змінна ціль аргумент_значення: адреса<Обʼєкт> = пусто;
    якщо аргументи == пусто {
      якщо іменовані_аргументи == пусто {
      } інакше {
        ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(іменовані_аргументи, М.БН.С.значення);
        якщо елемент_реєстру == пусто {
          // потім: помилка?
        } інакше {
          аргумент_значення = елемент_реєстру.значення;
        }
      }
    } інакше {
      якщо аргументи.розмір == 0 {
        якщо іменовані_аргументи == пусто {
        } інакше {
          ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(іменовані_аргументи, М.БН.С.значення);
          якщо елемент_реєстру == пусто {
            // потім: помилка?
          } інакше {
            аргумент_значення = елемент_реєстру.значення;
          }
        }
      } інакше {
        аргумент_значення = аргументи.дані[0];
      }
    }
    якщо аргумент_значення == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо додати пусто");
    }
    якщо аргумент_значення.тип != М.обʼєкт_структури_число {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо додати");
    }
    ціль оч1 = обʼєкт_я як адреса<ОбʼєктЧисла>;
    ціль оч2 = аргумент_значення як адреса<ОбʼєктЧисла>;
    вернути створити_число(М, оч1.значення + оч2.значення);
  }

  місцева дія створити_структуру_числа(М: адреса<Машина>): адреса<ОбʼєктСтруктури> {
    ціль обʼєкт_структури = створити_структуру(М, М.БН.С.Дія, М.обʼєкт_структури_обʼєкт);

    ціль обʼєкт_нативної_дії_додати = створити_нативну_дію(М, нативна_дія_числа_додати);
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.методи::адреса, М.БН.С.чародія_додати, обʼєкт_нативної_дії_додати як адреса<Обʼєкт>);

    вернути обʼєкт_структури;
  }

  місцева дія створити_число(М: адреса<Машина>, значення: д64): адреса<ОбʼєктЧисла> {
    ціль обʼєкт_числа = виділити<ОбʼєктЧисла>();
    обʼєкт_числа.наступний_обʼєкт = М.утилізатор.початок_обʼєктів;
    обʼєкт_числа.позначено = ні;
    обʼєкт_числа.тип = М.обʼєкт_структури_число;

    обʼєкт_числа.значення = значення;

    М.утилізатор.початок_обʼєктів = обʼєкт_числа як адреса<Обʼєкт>;
    М.утилізатор.кількість_обʼєктів = М.утилізатор.кількість_обʼєктів + 1;
    вернути обʼєкт_числа;
  }
}