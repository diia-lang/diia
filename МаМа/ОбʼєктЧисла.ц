////
// Автор це Богданович Давид Когут Прихожденко та Організація Мавки та Організація Організацій
////

взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт_числа_в_накопичувач_ю8(обʼєкт_числа: адреса<ОбʼєктЧисла>, М: адреса<Машина>, накопичувач_ю8: адреса<НакопичувачЮ8>, глибина: позитивне) {
    ціль д64ю8 = перетворити_д64_в_ю8(обʼєкт_числа.значення);
    покласти_в_накопичувач_ю8(накопичувач_ю8, д64ю8);
    звільнити(д64ю8);
  }

  місцева дія перевірити_чи_обʼєкт_я_і_аргумент_числа(М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): обʼєкт_або_стан_падіння {
    якщо обʼєкт_я == пусто {
      увімкнути_стан_падіння(М, пусто);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати пусто") як адреса<Обʼєкт>;
    }
    якщо обʼєкт_я.тип != М.обʼєкт_структури_число {
      увімкнути_стан_падіння(М, пусто);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати") як адреса<Обʼєкт>;
    }
    змінна ціль аргумент_значення: адреса<Обʼєкт> = пусто;
    якщо аргументи == пусто {
      якщо іменовані_аргументи == пусто {
      } інакше {
        ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(іменовані_аргументи, М.БН.С.значення);
        якщо елемент_реєстру == пусто {
          // потім: помилка?
        } інакше {
          аргумент_значення = елемент_реєстру.значення;
        }
      }
    } інакше {
      якщо аргументи.розмір == 0 {
        якщо іменовані_аргументи == пусто {
        } інакше {
          ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(іменовані_аргументи, М.БН.С.значення);
          якщо елемент_реєстру == пусто {
            // потім: помилка?
          } інакше {
            аргумент_значення = елемент_реєстру.значення;
          }
        }
      } інакше {
        аргумент_значення = аргументи.дані[0];
      }
    }
    якщо аргумент_значення == пусто {
      увімкнути_стан_падіння(М, пусто);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати пусто") як адреса<Обʼєкт>;
    }
    якщо аргумент_значення.тип != М.обʼєкт_структури_число {
      увімкнути_стан_падіння(М, пусто);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати") як адреса<Обʼєкт>;
    }
    вернути аргумент_значення як адреса<Обʼєкт>;
  }

  дія нативна_дія_числа_додати(обʼєкт_нативної_дії: адреса<ОбʼєктНативноїДії>, М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса<Обʼєкт> {
    ціль обʼєкт_аргумента = перевірити_чи_обʼєкт_я_і_аргумент_числа(М, обʼєкт_я, аргументи, іменовані_аргументи);
    якщо М.стан_падіння {
      вернути обʼєкт_аргумента;
    }
    ціль оч1 = обʼєкт_я як адреса<ОбʼєктЧисла>;
    ціль оч2 = обʼєкт_аргумента як адреса<ОбʼєктЧисла>;
    вернути виділити_обʼєкт_числа(М, оч1.значення + оч2.значення) як адреса<Обʼєкт>;
  }

  дія нативна_дія_числа_відняти(обʼєкт_нативної_дії: адреса<ОбʼєктНативноїДії>, М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса<Обʼєкт> {
    ціль обʼєкт_аргумента = перевірити_чи_обʼєкт_я_і_аргумент_числа(М, обʼєкт_я, аргументи, іменовані_аргументи);
    якщо М.стан_падіння {
      вернути обʼєкт_аргумента;
    }
    ціль оч1 = обʼєкт_я як адреса<ОбʼєктЧисла>;
    ціль оч2 = обʼєкт_аргумента як адреса<ОбʼєктЧисла>;
    вернути виділити_обʼєкт_числа(М, оч1.значення - оч2.значення) як адреса<Обʼєкт>;
  }

  дія нативна_дія_числа_помножити(обʼєкт_нативної_дії: адреса<ОбʼєктНативноїДії>, М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса<Обʼєкт> {
    ціль обʼєкт_аргумента = перевірити_чи_обʼєкт_я_і_аргумент_числа(М, обʼєкт_я, аргументи, іменовані_аргументи);
    якщо М.стан_падіння {
      вернути обʼєкт_аргумента;
    }
    ціль оч1 = обʼєкт_я як адреса<ОбʼєктЧисла>;
    ціль оч2 = обʼєкт_аргумента як адреса<ОбʼєктЧисла>;
    вернути виділити_обʼєкт_числа(М, оч1.значення * оч2.значення) як адреса<Обʼєкт>;
  }

  дія нативна_дія_числа_поділити(обʼєкт_нативної_дії: адреса<ОбʼєктНативноїДії>, М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса<Обʼєкт> {
    ціль обʼєкт_аргумента = перевірити_чи_обʼєкт_я_і_аргумент_числа(М, обʼєкт_я, аргументи, іменовані_аргументи);
    якщо М.стан_падіння {
      вернути обʼєкт_аргумента;
    }
    ціль оч1 = обʼєкт_я як адреса<ОбʼєктЧисла>;
    ціль оч2 = обʼєкт_аргумента як адреса<ОбʼєктЧисла>;
    вернути виділити_обʼєкт_числа(М, оч1.значення / оч2.значення) як адреса<Обʼєкт>;
  }

  місцева дія створити_структуру_числа(М: адреса<Машина>): адреса<ОбʼєктСтруктури> {
    ціль обʼєкт_структури = виділити_обʼєкт_структури(М, М.БН.С.число, М.обʼєкт_структури_обʼєкт);

    ціль обʼєкт_нативної_дії_додати = виділити_обʼєкт_нативної_дії(М, М.БН.С.чародія_додати, нативна_дія_числа_додати, пусто, пусто);
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.методи::адреса, М.БН.С.чародія_додати, обʼєкт_нативної_дії_додати як адреса<Обʼєкт>);
    ціль обʼєкт_нативної_дії_відняти = виділити_обʼєкт_нативної_дії(М, М.БН.С.чародія_відняти, нативна_дія_числа_відняти, пусто, пусто);
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.методи::адреса, М.БН.С.чародія_відняти, обʼєкт_нативної_дії_відняти як адреса<Обʼєкт>);
    ціль обʼєкт_нативної_дії_помножити = виділити_обʼєкт_нативної_дії(М, М.БН.С.чародія_помножити, нативна_дія_числа_помножити, пусто, пусто);
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.методи::адреса, М.БН.С.чародія_помножити, обʼєкт_нативної_дії_помножити як адреса<Обʼєкт>);
    ціль обʼєкт_нативної_дії_поділити = виділити_обʼєкт_нативної_дії(М, М.БН.С.чародія_поділити, нативна_дія_числа_поділити, пусто, пусто);
    змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.методи::адреса, М.БН.С.чародія_поділити, обʼєкт_нативної_дії_поділити як адреса<Обʼєкт>);

    вернути обʼєкт_структури;
  }

  місцева дія виділити_обʼєкт_числа(М: адреса<Машина>, значення: д64): адреса<ОбʼєктЧисла> {
    ціль обʼєкт_числа = виділити_обʼєкт<ОбʼєктЧисла>(М, М.обʼєкт_структури_число);

    обʼєкт_числа.значення = значення;

    вернути обʼєкт_числа;
  }

  місцева дія звільнити_обʼєкт_числа(обʼєкт_числа: адреса<ОбʼєктЧисла>, М: адреса<Машина>) {
    звільнити(обʼєкт_числа);
  }
}