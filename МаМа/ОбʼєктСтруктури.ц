////
// Автор це Богданович Давид Когут Прихожденко та Організація Мавки та Організація Організацій
////

взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт_структури_в_накопичувач_ю8(обʼєкт_структури: адреса<ОбʼєктСтруктури>, М: адреса<Машина>, накопичувач_ю8: адреса<НакопичувачЮ8>, глибина: позитивне) {
    покласти_в_накопичувач_ю8(накопичувач_ю8, ю8"<структура");
    якщо обʼєкт_структури.назва != пусто {
      покласти_в_накопичувач_ю8(накопичувач_ю8, ю8" ");
      ціль назва = отримати_ю8_з_назви(обʼєкт_структури.назва);
      покласти_в_накопичувач_ю8(накопичувач_ю8, назва);
      звільнити(назва);
    }
    покласти_в_накопичувач_ю8(накопичувач_ю8, ю8">");
  }

  місцева дія виділити_обʼєкт_структури(М: адреса<Машина>, назва: адреса<Назва>, предок: адреса<ОбʼєктСтруктури>): адреса<ОбʼєктСтруктури> {
    ціль обʼєкт_структури = виділити_обʼєкт<ОбʼєктСтруктури>(М, М.обʼєкт_структури_Структура);

    обʼєкт_структури.властивості = зробити_реєстр<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    обʼєкт_структури.назва = назва;
    обʼєкт_структури.предок = предок;
    обʼєкт_структури.параметри = виділити_реєстр<адреса<Назва>, Параметр>(перевірити_чи_назви_рівні);
    якщо предок != пусто {
      змінна ціль пп: позитивне = 0;
      поки пп < предок.параметри.розмір {
        змінити_в_реєстрі<адреса<Назва>, Параметр>(обʼєкт_структури.параметри, предок.параметри.елементи[пп].ключ, предок.параметри.елементи[пп].значення);
        пп += 1;
      }
    }
    обʼєкт_структури.методи = зробити_реєстр<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    обʼєкт_структури.середовище_створення = пусто;

    вернути обʼєкт_структури;
  }

  місцева дія обʼєкт_структури_виконати(обʼєкт_структури: адреса<ОбʼєктСтруктури>, М: адреса<Машина>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): обʼєкт_або_стан_падіння {
    ціль складений_обʼєкт = виділити_складений_обʼєкт(М, обʼєкт_структури);
    змінна ціль пп: позитивне = 0;
    поки пп < обʼєкт_структури.параметри.розмір {
      ціль елемент_параметра = обʼєкт_структури.параметри.елементи[пп];
      змінна ціль аргумент: адреса<Обʼєкт> = пусто;
      // потім: перевірити чи аргументи не пусто
      якщо аргументи.розмір > пп {
        аргумент = аргументи.дані[пп];
      } інакше {
        якщо іменовані_аргументи == пусто {
          якщо елемент_параметра.значення.код_значення_за_замовчуванням != пусто {
            ціль результат_виконання_значення_за_замовчуванням = виконати_код_в_середовищі(М, обʼєкт_структури.середовище_створення, елемент_параметра.значення.код_значення_за_замовчуванням);
            якщо М.стан_падіння {
              вернути результат_виконання_значення_за_замовчуванням;
            }
            ціль значення_за_замовчуванням = забрати_з_накопичувача<адреса<Обʼєкт>>(М.НаО::адреса);
            аргумент = значення_за_замовчуванням;
          }
        } інакше {
          ціль елемент_аргумента_з_назвою = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(іменовані_аргументи, елемент_параметра.ключ);
          якщо елемент_аргумента_з_назвою == пусто {
            якщо елемент_параметра.значення.код_значення_за_замовчуванням != пусто {
              ціль результат_виконання_значення_за_замовчуванням = виконати_код_в_середовищі(М, обʼєкт_структури.середовище_створення, елемент_параметра.значення.код_значення_за_замовчуванням);
              якщо М.стан_падіння {
                вернути результат_виконання_значення_за_замовчуванням;
              }
              ціль значення_за_замовчуванням = забрати_з_накопичувача<адреса<Обʼєкт>>(М.НаО::адреса);
              аргумент = значення_за_замовчуванням;
            }
          } інакше {
            аргумент = елемент_аргумента_з_назвою.значення;
          }
        }
      }
      якщо елемент_параметра.значення.типи.розмір > 0 {
        змінна ціль пп: позитивне = 0;
        змінна ціль знайдено_тип: логічне = ні;
        поки пп < елемент_параметра.значення.типи.розмір {
          якщо перевірити_чи_обʼєкт_є(аргумент, М, елемент_параметра.значення.типи.дані[пп]) {
            знайдено_тип = так;
          }
          пп += 1;
        }
        якщо знайдено_тип == ні {
          увімкнути_стан_падіння(М, пусто);
          якщо аргумент == пусто {
            вернути виділити_обʼєкт_тексту_помилки_невірний_тип_аргумента(М, елемент_параметра.ключ.значення, елемент_параметра.значення.типи, пусто);
          } інакше {
            вернути виділити_обʼєкт_тексту_помилки_невірний_тип_аргумента(М, елемент_параметра.ключ.значення, елемент_параметра.значення.типи, аргумент.тип);
          }
        }
      }
      змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(складений_обʼєкт.властивості::адреса, елемент_параметра.ключ, аргумент);
      пп += 1;
    }
    вернути складений_обʼєкт як адреса<Обʼєкт>;
  }

  місцева дія звільнити_обʼєкт_структури(обʼєкт_структури: адреса<ОбʼєктСтруктури>, М: адреса<Машина>) {
    звільнити_значення_реєстру<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.властивості);
    звільнити_реєстр<адреса<Назва>, Параметр>(обʼєкт_структури.параметри);
    звільнити_значення_реєстру<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.методи);
    звільнити(обʼєкт_структури);
  }
}