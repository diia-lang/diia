////
// Автор це Богданович Давид Когут Прихожденко та Організація Мавки та Організація Організацій
////

взяти визначення МаМа;

секція МаМа {
  місцева дія зробити_базу_назв(): БазаНазв {
    ціль БН: БазаНазв;
    БН.дані = зробити_накопичувач<адреса<Назва>>();
    БН.С.предок = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"предок");
    БН.С.чародія_додати = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"чародія_додати");
    БН.С.чародія_виконати = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"чародія_виконати");
    БН.С.обʼєкт = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"обʼєкт");
    БН.С.Структура = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"Структура");
    БН.С.НативнаДія = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"НативнаДія");
    БН.С.Дія = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"Дія");
    БН.С.число = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"число");
    БН.С.текст = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"текст");
    БН.С.друк = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"друк");
    БН.С.значення = знайти_або_виділити_й_додати_назву(БН::адреса, ю8"значення");
    вернути БН;
  }

  місцева дія знайти_або_виділити_й_додати_назву(БН: адреса<БазаНазв>, значення: Ю8): адреса<Назва> {
    змінна ціль позиція: позитивне = 0;
    поки позиція < БН.дані.розмір {
      ціль назва = БН.дані.дані[позиція];
      якщо перевірити_чи_ю8_рівні(назва.значення, значення) {
        вернути назва;
      }
      позиція = позиція + 1;
    }
    ціль назва = виділити_назву(значення);
    покласти_в_накопичувач<адреса<Назва>>(БН.дані::адреса, назва);
    вернути назва;
  }

  місцева дія додати_назву(БН: адреса<БазаНазв>, значення: Ю8): позитивне {
    змінна ціль позиція: позитивне = 0;
    поки позиція < БН.дані.розмір {
      ціль назва = БН.дані.дані[позиція];
      якщо перевірити_чи_ю8_рівні(назва.значення, значення) {
        вернути позиція;
      }
      позиція = позиція + 1;
    }
    ціль назва = виділити_назву(значення);
    покласти_в_накопичувач<адреса<Назва>>(БН.дані::адреса, назва);
    вернути БН.дані.розмір - 1;
  }

  місцева дія назва(М: адреса<Машина>, значення: Ю8): адреса<Назва> {
    вернути знайти_або_виділити_й_додати_назву(М.БН::адреса, значення);
  }

  місцева дія позиція_назви(М: адреса<Машина>, значення: Ю8): п32 {
    вернути додати_назву(М.БН::адреса, значення) як п32;
  }
}