взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт(М: адреса<Машина>, обʼєкт: адреса<Обʼєкт>) {
    якщо обʼєкт == пусто {
      надрукувати_ю8(ю8"пусто");
    } інакше якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_число {
      надрукувати_обʼєкт_числа(М, обʼєкт);
    } інакше якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_текст {
      надрукувати_обʼєкт_тексту(М, обʼєкт);
    } інакше {
      надрукувати_ю8(ю8"Невідомий обʼєкт");
    }
  }

  місцева дія обʼєкт_додати(я: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, аргумент: адреса<Обʼєкт>): адреса_або_стан_помилки<Обʼєкт> {
    якщо я.обʼєкт_структури == М.обʼєкт_структури_число {
      вернути обʼєкт_числа_додати(я, М, середовище, аргумент);
    }
    ціль властивість_чародії_додати = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(я.властивості::адреса, М.БН.чародія_додати);
    якщо властивість_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_додати не знайдено");
    }
    ціль обʼєкт_чародії_додати = властивість_чародії_додати.значення;
    якщо обʼєкт_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_додати є пусто");
    }
    ціль аргументи = виділити_вектор<адреса<Обʼєкт>>();
    додати_до_вектору<адреса<Обʼєкт>>(аргументи, аргумент);
    вернути обʼєкт_виконати(обʼєкт_чародії_додати, М, середовище, аргументи, пусто);
  }

  місцева дія обʼєкт_виконати(я: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, аргументи: адреса<Вектор<адреса<Обʼєкт>>>, аргументи_з_назвами: адреса_або_пусто<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса_або_стан_помилки<Обʼєкт> {
    якщо я.обʼєкт_структури == М.обʼєкт_структури_НативнаДія {
      вернути обʼєкт_нативної_дії_виконати(я, М, середовище, аргументи, аргументи_з_назвами);
    }
    ціль властивість_чародії_додати = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(я.властивості::адреса, М.БН.чародія_додати);
    якщо властивість_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_виконати не знайдено");
    }
    ціль обʼєкт_чародії_додати = властивість_чародії_додати.значення;
    якщо обʼєкт_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_виконати є пусто");
    }
    вернути обʼєкт_виконати(обʼєкт_чародії_додати, М, середовище, аргументи, аргументи_з_назвами);
  }

  місцева дія виділити_обʼєкт(М: адреса<Машина>, обʼєкт_структури: адреса<Обʼєкт>): адреса<Обʼєкт> {
    ціль обʼєкт = виділити<Обʼєкт>();
    обʼєкт.обʼєкт_структури = обʼєкт_структури;
    обʼєкт.властивості = зробити_реєстр<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    вернути обʼєкт;
  }
}