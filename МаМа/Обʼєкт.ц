взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт(М: адреса<Машина>, обʼєкт: адреса<Обʼєкт>) {
    якщо обʼєкт == пусто {
      надрукувати_ю8(ю8"пусто");
    } інакше якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_число {
      надрукувати_обʼєкт_числа(М, обʼєкт);
    } інакше якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_текст {
      надрукувати_обʼєкт_тексту(М, обʼєкт);
    } інакше {
      надрукувати_ю8(ю8"Невідомий обʼєкт");
    }
  }

  місцева дія обʼєкт_додати(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, аргумент: адреса<Обʼєкт>): адреса_або_стан_помилки<Обʼєкт> {
    якщо обʼєкт == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Неможливо додати пусто");
    }
    ціль обʼєкт_чародії_додати = обʼєкт.чародія_додати;
    якщо обʼєкт_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_помилки_чародію_не_знайдено(М, ю8"чародія_додати", обʼєкт);
    }
    ціль аргументи = виділити_вектор<адреса<Обʼєкт>>();
    додати_до_вектору<адреса<Обʼєкт>>(аргументи, аргумент);
    вернути обʼєкт_виконати(обʼєкт_чародії_додати, М, середовище, обʼєкт, аргументи, пусто);
  }

  місцева дія обʼєкт_виконати(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Вектор<адреса<Обʼєкт>>>, аргументи_з_назвами: адреса_або_пусто<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса_або_стан_помилки<Обʼєкт> {
    якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_НативнаДія {
      вернути (обʼєкт.дані як ДаніОбʼєктаНативноїДії)(обʼєкт, М, середовище, обʼєкт_я, аргументи, аргументи_з_назвами);
    }
    ціль обʼєкт_чародії_виконати = обʼєкт.чародія_виконати;
    якщо обʼєкт_чародії_виконати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_виконати є пусто");
    }
    вернути обʼєкт_виконати(обʼєкт_чародії_виконати, М, середовище, обʼєкт, аргументи, аргументи_з_назвами);
  }

  місцева дія виділити_обʼєкт(М: адреса<Машина>, обʼєкт_структури: адреса<Обʼєкт>): адреса<Обʼєкт> {
    ціль обʼєкт = виділити<Обʼєкт>();
    обʼєкт.обʼєкт_структури = обʼєкт_структури;
    обʼєкт.властивості = зробити_реєстр<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    обʼєкт.чародія_додати = пусто;
    обʼєкт.чародія_виконати = пусто;
    якщо обʼєкт_структури != пусто {
      синонім дані_структури = обʼєкт_структури.дані як ДаніОбʼєктаСтруктури;
      обʼєкт.чародія_додати = дані_структури.метод_чародія_додати;
      обʼєкт.чародія_виконати = дані_структури.метод_чародія_виконати;
    }
    вернути обʼєкт;
  }
}