взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт(М: адреса<Машина>, обʼєкт: адреса<Обʼєкт>) {
    якщо обʼєкт == пусто {
      надрукувати_ю8(ю8"пусто");
    } інакше якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_число {
      надрукувати_обʼєкт_число(М, обʼєкт);
    } інакше якщо обʼєкт.обʼєкт_структури == М.обʼєкт_структури_текст {
      надрукувати_обʼєкт_текст(М, обʼєкт);
    } інакше {
      надрукувати_ю8(ю8"Невідомий обʼєкт");
    }
  }

  дія обʼєкт_додати(я: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, аргумент: адреса<Обʼєкт>): адреса_або_стан_помилки<Обʼєкт> {
    ціль властивість_чародії_додати = отримати_з_карти<адреса<Назва>, адреса<Обʼєкт>>(я.властивості::адреса, М.БН.чародія_додати);
    якщо властивість_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_додати не знайдено");
    }
    ціль обʼєкт_чародії_додати = властивість_чародії_додати.значення;
    якщо обʼєкт_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_додати є пусто");
    }
    ціль аргументи = виділити_вектор<адреса<Обʼєкт>>();
    додати_до_вектору<адреса<Обʼєкт>>(аргументи, аргумент);
    вернути обʼєкт_чародії_додати.виконати_(обʼєкт_чародії_додати, М, середовище, аргументи, пусто);
  }

  дія обʼєкт_виконати_(я: адреса<Обʼєкт>, М: адреса<Машина>, середовище: адреса<Середовище>, аргументи: адреса<Вектор<адреса<Обʼєкт>>>, аргументи_з_назвами: адреса_або_пусто<Карта<адреса<Назва>, адреса<Обʼєкт>>>): адреса_або_стан_помилки<Обʼєкт> {
    ціль властивість_чародії_додати = отримати_з_карти<адреса<Назва>, адреса<Обʼєкт>>(я.властивості::адреса, М.БН.чародія_додати);
    якщо властивість_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_виконати не знайдено");
    }
    ціль обʼєкт_чародії_додати = властивість_чародії_додати.значення;
    якщо обʼєкт_чародії_додати == пусто {
      увімкнути_стан_помилки(М);
      вернути виділити_обʼєкт_тексту_з_ю8(М, ю8"Властивість чародія_виконати є пусто");
    }
    вернути обʼєкт_чародії_додати.виконати_(обʼєкт_чародії_додати, М, середовище, аргументи, аргументи_з_назвами);
  }

  місцева дія виділити_обʼєкт(М: адреса<Машина>, обʼєкт_структури: адреса<Обʼєкт>): адреса<Обʼєкт> {
    ціль обʼєкт = виділити<Обʼєкт>();
    обʼєкт.обʼєкт_структури = обʼєкт_структури;
    обʼєкт.властивості = зробити_карту<адреса<Назва>, адреса<Обʼєкт>>(перевірити_чи_назви_рівні);
    обʼєкт.додати = обʼєкт_додати;
    обʼєкт.виконати_ = обʼєкт_виконати_;
    вернути обʼєкт;
  }
}