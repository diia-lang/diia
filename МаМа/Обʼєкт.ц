взяти визначення МаМа;

секція МаМа {
  місцева дія надрукувати_обʼєкт(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, глибина: позитивне) {
    ціль буфер_ю8 = зробити_буфер_ю8();
    надрукувати_обʼєкт_в_буфер_ю8(обʼєкт, М, буфер_ю8::адреса, глибина);
    ціль ю8 = виділити_ю8_з_буфера(буфер_ю8::адреса);
    звільнити_значення_буфера_ю8(буфер_ю8);
    надрукувати_ю8(ю8);
    звільнити(ю8);
  }

  місцева дія надрукувати_обʼєкт_в_буфер_ю8(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, буфер_ю8: адреса<БуферЮ8>, глибина: позитивне) {
    якщо обʼєкт == пусто {
      додати_до_буфера_ю8(буфер_ю8, ю8"пусто");
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_число {
      надрукувати_обʼєкт_числа_в_буфер_ю8(обʼєкт як адреса<ОбʼєктЧисла>, М, буфер_ю8, глибина);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_текст {
      надрукувати_обʼєкт_тексту_в_буфер_ю8(обʼєкт як адреса<ОбʼєктТексту>, М, буфер_ю8, глибина);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_НативнаДія {
      надрукувати_обʼєкт_нативної_дії_в_буфер_ю8(обʼєкт як адреса<ОбʼєктНативноїДії>, М, буфер_ю8, глибина);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_Структура {
      надрукувати_обʼєкт_структури_в_буфер_ю8(обʼєкт як адреса<ОбʼєктСтруктури>, М, буфер_ю8, глибина);
    } інакше {
      додати_до_буфера_ю8(буфер_ю8, ю8"обʼєкт");
//      якщо обʼєкт.тип != пусто {
//        якщо обʼєкт.тип.назва != пусто {
//          додати_до_буфера_ю8(буфер_ю8, обʼєкт.тип.назва.значення);
//        }
//      }
//      додати_до_буфера_ю8(буфер_ю8, ю8"(");
//      змінна ціль пв: позитивне = 0;
//      поки пв < обʼєкт.властивості.розмір {
//        додати_до_буфера_ю8(буфер_ю8, обʼєкт.властивості.елементи[пв].ключ.значення);
//        додати_до_буфера_ю8(буфер_ю8, ю8"=");
//        надрукувати_обʼєкт_в_буфер_ю8(М, буфер_ю8, обʼєкт.властивості.елементи[пв].значення, глибина + 1);
//        якщо пв + 1 < обʼєкт.властивості.розмір {
//          додати_до_буфера_ю8(буфер_ю8, ю8", ");
//        }
//        пв = пв + 1;
//      }
//      додати_до_буфера_ю8(буфер_ю8, ю8")");
    }
  }

  місцева дія звільнити_обʼєкт(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>) {
    вивести_ю8(ю8"[МАВКА] звільнюємо обʼєкт ");
    надрукувати_обʼєкт(обʼєкт, М, 0);
    якщо обʼєкт.тип == М.обʼєкт_структури_Структура {
      звільнити_обʼєкт_структури(обʼєкт як адреса<ОбʼєктСтруктури>, М);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_Дія {
      звільнити_обʼєкт_дії(обʼєкт як адреса<ОбʼєктДії>, М);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_НативнаДія {
      звільнити_обʼєкт_нативної_дії(обʼєкт як адреса<ОбʼєктНативноїДії>, М);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_число {
      звільнити_обʼєкт_числа(обʼєкт як адреса<ОбʼєктЧисла>, М);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_текст {
      звільнити_обʼєкт_тексту(обʼєкт як адреса<ОбʼєктТексту>, М);
    } інакше {
      звільнити_складений_обʼєкт(обʼєкт як адреса<СкладенийОбʼєкт>, М);
    }
  }

  місцева дія перевірити_чи_обʼєкт_є(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, обʼєкт_структури: адреса<ОбʼєктСтруктури>): логічне {
    якщо обʼєкт == пусто {
      якщо обʼєкт_структури == пусто {
        вернути так;
      }
      вернути ні;
    }
    якщо обʼєкт_структури == пусто {
      вернути ні;
    }
    змінна ціль тип = обʼєкт.тип;
    поки тип != пусто {
      якщо тип == обʼєкт_структури {
        вернути так;
      }
      тип = тип.предок;
    }
    вернути ні;
  }

  місцева дія перевірити_чи_обʼєкт_так(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>): логічне {
    якщо обʼєкт == пусто {
      вернути ні;
    }
    якщо обʼєкт.тип == М.обʼєкт_структури_число {
      ціль обʼєкт_числа = обʼєкт як адреса<ОбʼєктЧисла>;
      якщо обʼєкт_числа.значення == 0.0 {
        вернути ні;
      }
    }
    // потім: перевірити ще логічне і можливо текст
    вернути так;
  }

  місцева дія обʼєкт_отримати_властивість(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, назва: адреса<Назва>): адреса<Обʼєкт> {
    якщо обʼєкт == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Намагаються отримати властивість з пусто");
    }
    якщо обʼєкт.тип == М.обʼєкт_структури_Структура {
      ціль обʼєкт_структури = обʼєкт як адреса<ОбʼєктСтруктури>;
      ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.властивості::адреса, назва);
      якщо елемент_реєстру != пусто {
        вернути елемент_реєстру.значення;
      }
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_Дія {
      ціль обʼєкт_дії = обʼєкт як адреса<ОбʼєктДії>;
      ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_дії.властивості::адреса, назва);
      якщо елемент_реєстру != пусто {
        вернути елемент_реєстру.значення;
      }
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_НативнаДія {
      ціль обʼєкт_нативної_дії = обʼєкт як адреса<ОбʼєктНативноїДії>;
      ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_нативної_дії.властивості::адреса, назва);
      якщо елемент_реєстру != пусто {
        вернути елемент_реєстру.значення;
      }
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_число {
      ціль обʼєкт_числа = обʼєкт як адреса<ОбʼєктЧисла>;
      змінна ціль тип = обʼєкт_числа.тип;
      поки тип != пусто {
        ціль елемент_реєстру_методів = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(тип.методи::адреса, назва);
        якщо елемент_реєстру_методів != пусто {
          вернути елемент_реєстру_методів.значення;
        }
        тип = тип.предок;
      }
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_текст {
      ціль обʼєкт_тексту = обʼєкт як адреса<ОбʼєктТексту>;
      змінна ціль тип = обʼєкт_тексту.тип;
      поки тип != пусто {
        ціль елемент_реєстру_методів = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(тип.методи::адреса, назва);
        якщо елемент_реєстру_методів != пусто {
          вернути елемент_реєстру_методів.значення;
        }
        тип = тип.предок;
      }
    } інакше {
      ціль складений_обʼєкт = обʼєкт як адреса<СкладенийОбʼєкт>;
      ціль елемент_реєстру = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(складений_обʼєкт.властивості::адреса, назва);
      якщо елемент_реєстру != пусто {
        вернути елемент_реєстру.значення;
      }
      змінна ціль тип = складений_обʼєкт.тип;
      поки тип != пусто {
        ціль елемент_реєстру_методів = отримати_з_реєстру<адреса<Назва>, адреса<Обʼєкт>>(тип.методи::адреса, назва);
        якщо елемент_реєстру_методів != пусто {
          вернути елемент_реєстру_методів.значення;
        }
        тип = тип.предок;
      }
    }
    вернути пусто;
  }

  місцева дія обʼєкт_змінити_властивість(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, назва: адреса<Назва>, аргумент: адреса<Обʼєкт>): адреса_або_стан_помилки<Обʼєкт> {
    якщо обʼєкт == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо змінити властивість пусто");
    }
    якщо обʼєкт.тип == М.обʼєкт_структури_Структура {
      ціль обʼєкт_структури = обʼєкт як адреса<ОбʼєктСтруктури>;
      змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_структури.властивості::адреса, назва, аргумент);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_Дія {
      ціль обʼєкт_дії = обʼєкт як адреса<ОбʼєктДії>;
      змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_дії.властивості::адреса, назва, аргумент);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_НативнаДія {
      ціль обʼєкт_нативної_дії = обʼєкт як адреса<ОбʼєктНативноїДії>;
      змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(обʼєкт_нативної_дії.властивості::адреса, назва, аргумент);
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_число {
    } інакше якщо обʼєкт.тип == М.обʼєкт_структури_текст {
    } інакше {
      ціль складений_обʼєкт = обʼєкт як адреса<СкладенийОбʼєкт>;
      якщо складений_обʼєкт.тип != пусто {
        змінна ціль параметр: адреса<Параметр> = пусто;
        змінна ціль пп: позитивне = 0;
        поки пп < складений_обʼєкт.тип.параметри.розмір {
          якщо strcmp(складений_обʼєкт.тип.параметри.елементи[пп].ключ.значення, назва.значення) == 0 {
            параметр = складений_обʼєкт.тип.параметри.елементи[пп].значення::адреса;
          }
          пп += 1;
        }
        якщо параметр != пусто {
          змінна ціль знайдено = ні;
          змінна ціль пт: позитивне = 0;
          поки пт < параметр.типи.розмір {
            ціль тип = параметр.типи.дані[пт];
            якщо перевірити_чи_обʼєкт_є(аргумент, М, тип) {
              знайдено = так;
            }
            пт += 1;
          }
          якщо знайдено == ні {
            увімкнути_стан_помилки(М);
            якщо аргумент == пусто {
              вернути виділити_обʼєкт_тексту_помилки_невірний_тип_аргумента(М, параметр.назва.значення, параметр.типи, пусто);
            } інакше {
              вернути виділити_обʼєкт_тексту_помилки_невірний_тип_аргумента(М, параметр.назва.значення, параметр.типи, аргумент.тип);
            }
          }
        }
      }
      змінити_в_реєстрі<адреса<Назва>, адреса<Обʼєкт>>(складений_обʼєкт.властивості::адреса, назва, аргумент);
    }
    вернути пусто;
  }

  місцева дія обʼєкт_додати(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, аргумент: адреса<Обʼєкт>): адреса_або_стан_помилки<Обʼєкт> {
    якщо обʼєкт == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо додати пусто");
    }
    ціль обʼєкт_чародії = обʼєкт_отримати_властивість(обʼєкт, М, М.БН.С.чародія_додати);
    якщо обʼєкт_чародії == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Вітання ви не можете додати це");
    }
    ціль аргументи = зробити_накопичувач<адреса<Обʼєкт>>();
    покласти_в_накопичувач<адреса<Обʼєкт>>(аргументи::адреса, аргумент);
    ціль результат_виконання = обʼєкт_виконати(обʼєкт_чародії, М, обʼєкт, аргументи::адреса, пусто);
    звільнити_значення_накопичувача<адреса<Обʼєкт>>(аргументи);
    вернути результат_виконання;
  }

  місцева дія обʼєкт_виконати(обʼєкт: адреса<Обʼєкт>, М: адреса<Машина>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): адреса_або_стан_помилки<Обʼєкт> {
    якщо обʼєкт == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Неможливо виконати пусто");
    }
    якщо обʼєкт.тип == М.обʼєкт_структури_Структура {
      вернути обʼєкт_структури_виконати(обʼєкт як адреса<ОбʼєктСтруктури>, М, аргументи, іменовані_аргументи);
    }
    якщо обʼєкт.тип == М.обʼєкт_структури_Дія {
      вернути обʼєкт_дії_виконати(обʼєкт як адреса<ОбʼєктДії>, М, обʼєкт_я, аргументи, іменовані_аргументи);
    }
    якщо обʼєкт.тип == М.обʼєкт_структури_НативнаДія {
      вернути обʼєкт_нативної_дії_виконати(обʼєкт як адреса<ОбʼєктНативноїДії>, М, обʼєкт_я, аргументи, іменовані_аргументи);
    }
    ціль обʼєкт_чародії_виконати = обʼєкт_отримати_властивість(обʼєкт, М, М.БН.С.чародія_виконати);
    якщо обʼєкт_чародії_виконати == пусто {
      увімкнути_стан_помилки(М);
      вернути створити_текст_з_ю8(М, ю8"Вітання ви не можете виконати це");
    }
    вернути обʼєкт_виконати(обʼєкт_чародії_виконати, М, обʼєкт_я, аргументи, іменовані_аргументи);
  }
}