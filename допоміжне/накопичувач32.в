////
// Автор це Богданович Давид Когут Прихожденко та Організація Мавки та Організація Організацій
////

взяти визначення допоміжне/біб;

структура Накопичувач32<Т>;

структура Накопичувач32<Т> {
  розмір: п32;
  дані: памʼять<Т>;
}

дія зробити_накопичувач32<Т>(): Накопичувач32<Т> {
  ціль накопичувач32: Накопичувач32<Т>;
  накопичувач32.розмір = 0;
  накопичувач32.дані = пусто;
  вернути накопичувач32;
}

дія виділити_накопичувач32<Т>(): адреса<Накопичувач32<Т>> {
  ціль накопичувач32 = виділити<Накопичувач32<Т>>();
  накопичувач32.розмір = 0;
  накопичувач32.дані = пусто;
  вернути накопичувач32;
}

дія покласти_в_накопичувач32<Т>(накопичувач32: адреса<Накопичувач32<Т>>, дані: Т) {
  накопичувач32.дані = перевиділити<Т>(накопичувач32.дані, (накопичувач32.розмір + 1) як ц32);
  накопичувач32.дані[накопичувач32.розмір] = дані;
  накопичувач32.розмір = накопичувач32.розмір + 1;
}

дія забрати_з_накопичувача32<Т>(накопичувач32: адреса<Накопичувач32<Т>>): Т {
  ціль дані = накопичувач32.дані[накопичувач32.розмір - 1];
  накопичувач32.розмір = накопичувач32.розмір - 1;
  вернути дані;
}

дія звільнити_значення_накопичувача32<Т>(накопичувач32: Накопичувач32<Т>) {
  звільнити(накопичувач32.дані);
}

дія звільнити_накопичувач32<Т>(накопичувач32: адреса<Накопичувач32<Т>>) {
  звільнити(накопичувач32.дані);
  звільнити(накопичувач32);
}

дія обернути_накопичувач32<Т>(накопичувач32: адреса<Накопичувач32<Т>>) {
  якщо накопичувач32.розмір == 0 {
    вернути;
  }
  змінна ціль па: п32 = 0;
  змінна ціль пб: п32 = накопичувач32.розмір - 1;
  поки па < пб {
    змінна ціль тимчасове = накопичувач32.дані[па];
    накопичувач32.дані[па] = накопичувач32.дані[пб];
    накопичувач32.дані[пб] = тимчасове;
    па = па + 1;
    пб = пб - 1;
  }
}