взяти визначення мавка;

дія нативна_дія_мавки_друк(обʼєкт_нативної_дії: адреса<мавка::Обʼєкт>, В: адреса<мавка::Виконувач>, обʼєкт_я: адреса<мавка::Обʼєкт>, аргументи: адреса<Накопичувач<адреса<мавка::Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<мавка::Назва>, адреса<мавка::Обʼєкт>>>): мавка::Результат {
  якщо аргументи != пусто {
    змінна ціль па: позитивне = 0;
    поки па < аргументи.розмір {
      ціль накопичувач_ю8 = зробити_накопичувач_ю8();
      МаМа::надрукувати_обʼєкт_в_накопичувач_ю8(аргументи.дані[па] як адреса<МаМа::Обʼєкт>, В.М, накопичувач_ю8::адреса, 0);
      ціль ю8 = виділити_ю8_з_накопичувача_ю8(накопичувач_ю8::адреса);
      звільнити_значення_накопичувача_ю8(накопичувач_ю8);
      надрукувати_ю8(ю8);
      звільнити(ю8);
      па += 1;
    }
  }

  вернути мавка::Результат { пусто, пусто };
}

зовнішня дія стартувати_мавку(кількість_аргументів: ц32, аргументи: памʼять<Ю8>): ц32 {
  ціль В = мавка::створити_виконувач();
  ціль глобальне_середовище = мавка::отримати_глобальне_середовище(В);
  мавка::визначити_нативну_дію(В, глобальне_середовище, ю8"друк", нативна_дія_мавки_друк);
  мавка::взяти_модуль(В, ю8"привіт");
  ціль тимчасове_серередовище = мавка::покласти_тимчасове_середовище(В);
  мавка::почати_діалог(В, тимчасове_серередовище);
  мавка::забрати_тимчасове_середовище(В);
  мавка::знищити_виконувач(В);
  вернути 0;
}