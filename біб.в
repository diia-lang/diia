синонім невідома_комірка = адреса;
синонім невідома_памʼять = адреса;
синонім комірка<Т> = адреса<Т>;
синонім памʼять<Т> = адреса<Т>;

зовнішня дія malloc(size: ц32): невідома_памʼять;
зовнішня дія free(ptr: адреса);
зовнішня дія realloc(ptr: адреса, size: ц32): невідома_памʼять;

дія виділити<Т>(): комірка<Т>;
дія виділити<Т>(кількість: ц32): памʼять<Т>;
дія перевиділити<Т>(значення: адреса<Т>, кількість: ц32): памʼять<Т>;
дія звільнити(значення: адреса);

дія виділити<Т>(): комірка<Т> {
  вернути malloc(Т::розмір) як комірка<Т>;
}

дія виділити<Т>(кількість: ц32): памʼять<Т> {
  вернути malloc(кількість * Т::розмір) як памʼять<Т>;
}

дія перевиділити<Т>(значення: адреса<Т>, кількість: ц32): памʼять<Т> {
  вернути realloc(значення, кількість * Т::розмір) як памʼять<Т>;
}

дія звільнити(значення: невідома_комірка) {
  free(значення);
}

зовнішня дія write(fd: ц32, buf: адреса, count: ц32): ц32;

дія надрукувати_ю8(значення: памʼять<п8>) {
  змінна ціль х: позитивне = 0;
  поки (значення[х] != 0) {
    write(1, значення + х, 1);
    х = х + 1;
  }
  ціль нр = 10п8;
  write(1, нр::адреса, 1);
}

дія перетворити_п64_в_ю8(значення: п64): памʼять<п8> {
  змінна ціль буфер: памʼять<п8> = виділити<п8>(21);
  змінна ціль індекс: ц32 = 20;
  буфер[20] = 0;
  поки (значення > 0) {
    індекс = індекс - 1;
    буфер[індекс] = (значення % 10) як п8 + 48;
    значення = значення / 10;
  }
  якщо (індекс == 20) {
    індекс = індекс - 1;
    буфер[індекс] = 48;
  }
  ціль результуючий_буфер: памʼять<п8> = виділити<п8>(21ц32 - індекс);
  змінна ціль х: ц32 = 0;
  поки (індекс < 21) {
    результуючий_буфер[х] = буфер[індекс];
    х = х + 1;
    індекс = індекс + 1;
  }
  звільнити(буфер);
  вернути результуючий_буфер;
}

дія перетворити_д64_в_ю8(значення: д64): памʼять<п8> {
  вернути перетворити_п64_в_ю8(значення як п64); // потім: пофіксати
}

дія надрукувати_п64(значення: п64) {
  ціль п64ю8 = перетворити_п64_в_ю8(значення);
  надрукувати_ю8(п64ю8);
  звільнити(п64ю8);
}

дія надрукувати_д64(значення: д64) {
  ціль д64ю8 = перетворити_д64_в_ю8(значення);
  надрукувати_ю8(д64ю8);
  звільнити(д64ю8);
}

дія fmod(a: д64, b: д64): д64 {
  вернути a - (a / b) * b;
}

дія перевірити_чи_ю8_рівні(а: памʼять<п8>, б: памʼять<п8>): логічне {
  змінна ціль позиція: позитивне = 0;
  поки а[позиція] == б[позиція] {
    якщо а[позиція] == 0 {
      вернути так;
    }
    позиція = позиція + 1;
  }
  вернути ні;
}