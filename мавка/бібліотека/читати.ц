взяти визначення бібліотека;

зовнішня дія читати_зі_стандартного_вводу(префікс: памʼять<п8>): памʼять<п8>;

секція мама {
  внутрішня дія нативна_дія_читати(М: комірка<Машина>, місцезнаходження: комірка<Місцезнаходження>, обʼєкт_нативної_дії: комірка<Обʼєкт>, аргументи: комірка<вектор<Аргумент>>): Результат {
    змінна префікс_ю8 = ю8"";
    якщо аргументи.довжина > 0 {
      ціль значення_першого_аргумента = аргументи.дані[0].значення;
      якщо значення_першого_аргумента.тип != ЗОбʼєкт {
        вернути результат_помилка_обʼєкт(створити_обʼєкт_помилка(М, місцезнаходження, ю8"Перший аргумент має бути текстом"));
      }
      ціль обʼєкт = значення_першого_аргумента.дані як комірка<Обʼєкт>;
      якщо обʼєкт.тип != ОТекст {
        вернути результат_помилка_обʼєкт(створити_обʼєкт_помилка(М, місцезнаходження, ю8"Перший аргумент має бути текстом"));
      }
      префікс_ю8 = ю32_в_ю8(обʼєкт.текст_значення);
    }
    ціль ввід = читати_зі_стандартного_вводу(префікс_ю8);
    якщо ввід == пусто {
      вернути результат_значення(Значення { ЗПусто });
    }
    ціль обʼєкт_тексту = створити_обʼєкт_текст(М, ввід);
    вернути результат_значення(Значення { ЗОбʼєкт, дані = обʼєкт_тексту });
  }

  місцева дія завантажити_біб_читати(М: комірка<Машина>, місцезнаходження: комірка<Місцезнаходження>, середовище: комірка<Середовище>, обʼєкт_модуля: комірка<Обʼєкт>): Результат {
    змінна обʼєкт_нативної_дії_читати = створити_обʼєкт_нативна_дія(М, нативна_дія_читати);
    змінити_властивість_обʼєкта(обʼєкт_модуля, отримати_або_створити_слово(М, ю8"чародія_виконати"), Значення { ЗОбʼєкт, дані = обʼєкт_нативної_дії_читати });
    вернути результат_значення(Значення { ЗПусто });
  }
}