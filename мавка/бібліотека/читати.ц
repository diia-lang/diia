взяти визначення мавка/бібліотека;

секція мавка {
  секція бібліотека {
    дія нативна_дія_бібліотеки_мавки_читати(обʼєкт_нативної_дії: адреса<Обʼєкт>, В: адреса<Виконувач>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<ІменованіАргументи>, дані: адреса): Результат {
      змінна ціль знайдено_аргумент_префікс = ні;
      змінна ціль аргумент_префікс: адреса<Обʼєкт> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_префікс = знайти_іменований_аргумент(В, іменовані_аргументи, В.М.БН.С.префікс як адреса<Назва>, аргумент_префікс::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_префікс == ні {
          якщо аргументи.розмір > 0 {
            знайдено_аргумент_префікс = так;
            аргумент_префікс = аргументи.дані[0];
          }
        }
      }
      змінна ціль розмір_префіксу: позитивне = 0;
      змінна ціль префікс: памʼять<п8> = пусто;
      якщо знайдено_аргумент_префікс {
        якщо перевірити_чи_обʼєкт_є_юнікодом(аргумент_префікс, В) {
          якщо скопіювати_значення_обʼєкта_юнікоду_як_Ю8(аргумент_префікс, В, розмір_префіксу::адреса, префікс::адреса) == ні {
            вернути результат_падіння(В, пусто, пусто);
          }
        } інакше {
          вернути результат_падіння(В, створити_текст_з_Ю8(В, ю8"Префікс має бути юнікодом"), пусто);
        }
      }
      ціль значення: памʼять<п8> = пусто;
      ціль довжина_значення = прочитати_з_stdin(префікс, значення::адреса);
      звільнити(В, префікс як адреса);
      якщо значення == пусто {
        вернути результат_успіх(В, пусто);
      } інакше {
        ціль обʼєкт_юнікоду = створити_юнікод_з_Ю8(В, значення);
        звільнити(В, значення як адреса);
        вернути результат_успіх(В, обʼєкт_юнікоду);
      }
    }

    місцева дія заповнити_модуль_читати(В: адреса<Виконувач>, обʼєкт_модуля: адреса<Обʼєкт>) {
      обʼєкт_змінити_властивість(обʼєкт_модуля, В, назва_з_Ю8(В, ю8"чародія_виконати"), створити_нативну_дію(В, назва_з_Ю8(В, ю8"читати"), нативна_дія_бібліотеки_мавки_читати, пусто, пусто));
    }
  }
}