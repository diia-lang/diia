////
// Автор це Богданович Давид Когут Прихожденко та Організація Мавки та Організація Організацій
////

взяти визначення мавка/бібліотека;
взяти визначення РМв1;

зовнішня дія mavka_load_shared_object_function_ptr_from_file(path: адреса<unsigned_char>, name: адреса<unsigned_char>): адреса;

секція мавка {
  секція бібліотека {
    дія РМв1_надрукувати_Ю8(значення: памʼять<п8>, Р: адреса<РМв1::Розширення>) {
      МаМа::надрукувати_ю8(значення);
    }

    дія РМв1_надрукувати_обʼєкт(обʼєкт: адреса<РМв1::Обʼєкт>, Р: адреса<РМв1::Розширення>, глибина: позитивне) {
      ціль В = Р.В як адреса<Виконувач>;
      ціль М = В.М;
      МаМа::надрукувати_обʼєкт(обʼєкт як адреса<МаМа::Обʼєкт>, М, глибина);
    }

    місцева дія нативна_дія_модуля_мавка_завантажити_РМв1(обʼєкт_нативної_дії: адреса<Обʼєкт>, В: адреса<Виконувач>, обʼєкт_я: адреса<Обʼєкт>, аргументи: адреса<Накопичувач<адреса<Обʼєкт>>>, іменовані_аргументи: адреса<Реєстр<адреса<Назва>, адреса<Обʼєкт>>>): Результат {
      якщо аргументи.розмір > 0 {
        ціль обʼєкт_аргумента_шляху = аргументи.дані[0];
        ціль розмір_ю8_шляху: позитивне = 0;
        ціль ю8_шляху: Ю8 = пусто;
        якщо скомпіювати_значення_обʼкта_юнікоду_як_Ю8(обʼєкт_аргумента_шляху, В, розмір_ю8_шляху::адреса, ю8_шляху::адреса) == ні {
          вернути результат_падіння(пусто, пусто);
        }
        МаМа::надрукувати_ю8(ю8_шляху);
        ціль завантажити_РМв1 = mavka_load_shared_object_function_ptr_from_file(ю8_шляху, ю8"завантажити_РМв1") як (Р: адреса<РМв1::Розширення>) -> ніщо;
        якщо завантажити_РМв1 == пусто {
          МаМа::надрукувати_ю8(ю8"Помилка завантаження функції з файлу");
          вернути результат_падіння(пусто, пусто);
        }
        ціль розширення = виділити<РМв1::Розширення>();
        розширення.версія = РМв1::ВерсіяРозширення {
          мажор = РМв1::МАЖОР,
          мінор = РМв1::МІНОР,
          патч = РМв1::ПАТЧ
        };
        розширення.дані = пусто;
        розширення.В = В як адреса<РМв1::Виконувач>;
        розширення.обʼєкт_модуля_розширення = пусто;
        розширення.обʼєкт_логічного_так = пусто;
        розширення.обʼєкт_логічного_ні = пусто;
        розширення.надрукувати_Ю8 = РМв1_надрукувати_Ю8;
        розширення.надрукувати_обʼєкт = РМв1_надрукувати_обʼєкт;
        розширення.взяти_файл = пусто;
        розширення.отримати_глобальне_середовище = пусто;
        розширення.покласти_тимчасове_середовище = пусто;
        розширення.забрати_тимчасове_середовище = пусто;
        розширення.покласти_в_НатО = пусто;
        розширення.забрати_з_НатО = пусто;
        розширення.назва_з_Ю8 = пусто;
        розширення.створити_нативну_дію = пусто;
        розширення.створити_число = пусто;
        розширення.створити_текст_з_ю8 = пусто;
        розширення.створити_юнікод_з_ю8 = пусто;
        розширення.створити_модуль = пусто;
        розширення.створити_список = пусто;
        розширення.створити_словник = пусто;
        розширення.створити_обʼєкт = пусто;
        завантажити_РМв1(розширення);
      }
      вернути результат_успіх(пусто);
    }

    місцева дія заповнити_модуль_мавка(В: адреса<Виконувач>, обʼєкт_модуля: адреса<Обʼєкт>) {
      обʼєкт_змінити_властивість(обʼєкт_модуля, В, назва_з_ю8(В, ю8"завантажити_РМв1"), створити_нативну_дію(В, назва_з_ю8(В, ю8"завантажити_РМв1"), нативна_дія_модуля_мавка_завантажити_РМв1));
    }
  }
}