взяти визначення МаМа;

секція мама {
  місцева дія створити_середовище(М: комірка<Машина>, зовнішнє: комірка<Середовище>): комірка<Середовище> {
    ціль середовище = виділити<Середовище>();
    середовище.наступне_середовище = М.утилізатор.початок_середовищ;
    середовище.помарковано = 0;
    середовище.зовнішнє = зовнішнє;
    середовище.субʼєкти = створити_карту<комірка<Слово>, Значення>(порівняти_слова);
    середовище.обʼєкт_дії = пусто;
    середовище.обʼєкт_модуля = пусто;
    М.утилізатор.початок_середовищ = середовище;
    М.утилізатор.кількість_середовищ = М.утилізатор.кількість_середовищ + 1;
    вернути середовище;
  }

  місцева дія звільнити_середовище(середовище: комірка<Середовище>) {
    звільнити_карту<комірка<Слово>, Значення>(середовище.субʼєкти);
    звільнити(середовище);
  }

  місцева дія отримати_субʼєкт_зі_середовища(середовище: комірка<Середовище>, слово: комірка<Слово>): Значення {
    якщо перевірити_чи_карта_містить_значення<комірка<Слово>, Значення>(середовище.субʼєкти, слово) {
      вернути отримати_значення_з_карти<комірка<Слово>, Значення>(середовище.субʼєкти, слово, Значення { ЗПусто });
    }
    якщо середовище.зовнішнє != пусто {
      вернути отримати_субʼєкт_зі_середовища(середовище.зовнішнє, слово);
    }
    вернути Значення { ЗПусто };
  }

  місцева дія змінити_субʼєкт_в_середовищі(середовище: комірка<Середовище>, слово: комірка<Слово>, значення: Значення) {
    змінити_значення_карти<комірка<Слово>, Значення>(
      середовище.субʼєкти,
      слово,
      значення
    );
  }

  місцева дія помаркувати_середовище(середовище: комірка<Середовище>) {
    якщо середовище.помарковано == 0 {
      середовище.помарковано = 1;
      змінна х = 0;
      поки х < середовище.субʼєкти.елементи.довжина {
        ціль ел = середовище.субʼєкти.елементи.дані[х];
        ціль значення = ел.значення;
        якщо значення.тип == ЗОбʼєкт {
          помаркувати_обʼєкт(значення.дані як комірка<Обʼєкт>);
        }
        х = х + 1;
      }
      якщо середовище.зовнішнє != пусто {
        помаркувати_середовище(середовище.зовнішнє);
      }
    }
  }
}