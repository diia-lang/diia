взяти визначення МаМа;

секція мама {
  місцева дія створити_обʼєкт_модуль(М: комірка<Машина>, слово_назви: комірка<Слово>): комірка<Обʼєкт> {
    ціль обʼєкт_модуля = створити_обʼєкт(М, ОМодуль);
    обʼєкт_модуля.с = М.структура_модуль;
    обʼєкт_модуля.модуль_слово = слово_назви;
    вернути обʼєкт_модуля;
  }

  дія структура_модуль_виконати_дія(М: комірка<Машина>, місцезнаходження: комірка<Місцезнаходження>, обʼєкт: комірка<Обʼєкт>, аргументи: комірка<вектор<Аргумент>>): Результат {
    якщо аргументи.довжина < 1 {
      вернути результат_помилка_обʼєкт(створити_помилку_П21_неможливо_виконати(М, місцезнаходження, Значення { ЗОбʼєкт, дані = обʼєкт }));
    }
    ціль значення_першого_аргумента = аргументи.дані[0].значення;
    якщо значення_першого_аргумента.тип == ЗОбʼєкт {
      ціль обʼєкт_значення_першого_аргумента = значення_першого_аргумента.дані як комірка<Обʼєкт>;
      якщо обʼєкт_значення_першого_аргумента.тип == ОСписок {
        вернути результат_значення(значення_першого_аргумента);
      }
      ціль результат_отримання_чародії_модуль = обʼєкт_значення_першого_аргумента.отримати_властивість(М, місцезнаходження, обʼєкт_значення_першого_аргумента, отримати_або_створити_слово(М, ю8"чародія_модуль"));
      якщо результат_отримання_чародії_модуль.успіх == ні {
        вернути результат_отримання_чародії_модуль;
      }
      вернути машина_виконати_дію(М, місцезнаходження, результат_отримання_чародії_модуль.значення, 0);
    }
    вернути результат_помилка_обʼєкт(створити_помилку_П21_неможливо_виконати(М, місцезнаходження, Значення { ЗОбʼєкт, дані = обʼєкт }));
  }

  місцева дія створити_структуру_модуль(М: комірка<Машина>): комірка<Обʼєкт> {
    ціль слово = отримати_або_створити_слово(М, ю8"Модуль");
    ціль обʼєкт_структури = створити_обʼєкт_структура(М, слово, М.структура_обʼєкт);
    обʼєкт_структури.виконати = структура_модуль_виконати_дія;
    вернути зробити_безсмертним(М, обʼєкт_структури);
  }
}