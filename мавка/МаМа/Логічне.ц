взяти визначення МаМа;

секція мама {
  дія нативна_дія_методу_структури_логічне_чародія_число(М: комірка<Машина>, місцезнаходження: комірка<Місцезнаходження>, обʼєкт_нативної_дії: комірка<Обʼєкт>, аргументи: комірка<вектор<Аргумент>>): Результат {
    вернути результат_помилка_обʼєкт(створити_помилку_П22_неможливо_виконати_дію(М, місцезнаходження, ю8"чародія_число"));
  }

  дія нативна_дія_методу_структури_логічне_чародія_текст(М: комірка<Машина>, місцезнаходження: комірка<Місцезнаходження>, обʼєкт_нативної_дії: комірка<Обʼєкт>, аргументи: комірка<вектор<Аргумент>>): Результат {
    вернути результат_помилка_обʼєкт(створити_помилку_П22_неможливо_виконати_дію(М, місцезнаходження, ю8"чародія_число"));
  }

  дія структура_логічне_виконати_дія(М: комірка<Машина>, місцезнаходження: комірка<Місцезнаходження>, обʼєкт: комірка<Обʼєкт>, аргументи: комірка<вектор<Аргумент>>): Результат {
    якщо аргументи.довжина < 1 {
      вернути результат_помилка_обʼєкт(створити_помилку_П21_неможливо_виконати(М, місцезнаходження, Значення { ЗОбʼєкт, дані = обʼєкт }));
    }
    ціль значення_першого_аргумента = аргументи.дані[0].значення;
    якщо значення_першого_аргумента.тип == ЗПусто {
      вернути результат_значення(Значення { ЗНі });
    }
    якщо значення_першого_аргумента.тип == ЗТак {
      вернути результат_значення(Значення { ЗТак });
    }
    якщо значення_першого_аргумента.тип == ЗНі {
      вернути результат_значення(Значення { ЗНі });
    }
    якщо значення_першого_аргумента.тип == ЗЧисло {
      якщо (значення_першого_аргумента.дані як дійсне) == 0.0 {
        вернути результат_значення(Значення { ЗНі });
      }
      вернути результат_значення(Значення { ЗТак });
    }
    якщо значення_першого_аргумента.тип == ЗСимвол {
      вернути результат_значення(Значення { ЗТак });
    }
    якщо значення_першого_аргумента.тип == ЗОбʼєкт {
      вернути результат_значення(Значення { ЗТак });
    }
    вернути результат_помилка_обʼєкт(створити_помилку_П21_неможливо_виконати(М, місцезнаходження, Значення { ЗОбʼєкт, дані = обʼєкт }));
  }

  місцева дія створити_структуру_логічне(М: комірка<Машина>): комірка<Обʼєкт> {
    ціль слово = отримати_або_створити_слово(М, ю8"логічне");
    ціль обʼєкт_структури = створити_обʼєкт_структура(М, слово, М.структура_обʼєкт);
    обʼєкт_структури.виконати_як_дію = структура_логічне_виконати_дія;
    ціль обʼєкт_нативної_дії_чародія_число = зробити_безсмертним(М, створити_обʼєкт_нативна_дія(М, нативна_дія_методу_структури_логічне_чародія_число));
    обʼєкт_нативної_дії_чародія_число.дія_слово = отримати_або_створити_слово(М, ю8"чародія_число");
    змінити_значення_карти<комірка<Слово>, комірка<Обʼєкт>>(обʼєкт_структури.структура_методи, отримати_або_створити_слово(М, ю8"чародія_число"), обʼєкт_нативної_дії_чародія_число);
    ціль обʼєкт_нативної_дії_чародія_текст = зробити_безсмертним(М, створити_обʼєкт_нативна_дія(М, нативна_дія_методу_структури_логічне_чародія_текст));
    обʼєкт_нативної_дії_чародія_текст.дія_слово = отримати_або_створити_слово(М, ю8"чародія_текст");
    змінити_значення_карти<комірка<Слово>, комірка<Обʼєкт>>(обʼєкт_структури.структура_методи, отримати_або_створити_слово(М, ю8"чародія_текст"), обʼєкт_нативної_дії_чародія_текст);
    вернути зробити_безсмертним(М, обʼєкт_структури);
  }
}