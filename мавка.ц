взяти визначення МаМа;

зовнішня дія прочитати_файл(шлях: Ю8): Ю8;

дія виконати_друк(я: адреса<МаМа::Обʼєкт>, М: адреса<МаМа::Машина>, середовище: адреса<МаМа::Середовище>, аргументи: адреса<Вектор<адреса<МаМа::Обʼєкт>>>, аргументи_з_назвами: адреса<Реєстр<адреса<МаМа::Назва>, адреса<МаМа::Обʼєкт>>>): адреса<МаМа::Обʼєкт> {
  змінна ціль па: позитивне = 0;
  поки па < аргументи.довжина {
    МаМа::надрукувати_обʼєкт(М, аргументи.дані[па]);
    па = па + 1;
  }
  вернути пусто;
}

зовнішня дія стартувати_мавку(кількість_аргументів: ц32, аргументи: памʼять<Ю8>): ц32 {
  ціль шлях = ю8"привіт.м";
  ціль значення_з_файлу = прочитати_файл(шлях);
  ціль текст_коду = виділити<ТекстКоду>();
  текст_коду.шлях = виділити_копію_ю8(шлях);
  текст_коду.значення = значення_з_файлу;
  ціль результат_розбору_мавки = розібрати_мавку(текст_коду);
  якщо результат_розбору_мавки.успіх == 0 {
    вивести_ю8(ю8"Помилка розбору: ");
    надрукувати_ю8(результат_розбору_мавки.помилка.повідомлення);
    вернути 1;
  }
  ціль М = МаМа::виділити_машину();
  ціль обʼєкт_дії_друк = МаМа::виділити_обʼєкт_нативної_дії(М, виконати_друк);
  МаМа::змінити_в_середовищі(М.глобальне_середовище, МаМа::знайти_або_виділити_й_додати_назву(М.БН::адреса, ю8"друк"), обʼєкт_дії_друк);
  ціль код = МаМа::виділити_код(текст_коду);
  ціль контекст_компіляції = МаМа::виділити_контекст_компіляції();
  ціль помилка_компіляції = МаМа::скомпілювати_тіло(М, контекст_компіляції, код, результат_розбору_мавки.тіло);
  якщо помилка_компіляції != пусто {
    надрукувати_ю8(помилка_компіляції.повідомлення);
    вернути 1;
  }
  МаМа::надрукувати_код(код);
  ціль результат_виконання_коду = МаМа::виконати_код(М, код);
  якщо М.стан_помилки {
    якщо М.місцезнаходження_помилки != пусто {
      вивести_ю8(М.місцезнаходження_помилки.текст_коду.шлях);
      вивести_ю8(ю8":");
      вивести_п64(М.місцезнаходження_помилки.рядок як п64);
      вивести_ю8(ю8":");
      вивести_п64(М.місцезнаходження_помилки.стовпець як п64);
      вивести_ю8(ю8": ");
    } інакше {
      вивести_ю8(ю8"Помилка виконання: ");
    }
    МаМа::надрукувати_обʼєкт(М, результат_виконання_коду);
  }
  вернути 0;
}