взяти визначення МаМа;

зовнішня дія прочитати_файл(шлях: Ю8): Ю8;

дія виконати_друк(я: адреса<МаМа::Обʼєкт>, М: адреса<МаМа::Машина>, середовище: адреса<МаМа::Середовище>, аргументи: адреса<Вектор<адреса<МаМа::Обʼєкт>>>, накопичувач_аргументів_з_назвами: адреса<МаМа::НакопичувачАргументів>): адреса<МаМа::Обʼєкт> {
  надрукувати_д64(аргументи.дані[0].дані);
  вернути пусто;
}

зовнішня дія стартувати_мавку(кількість_аргументів: ц32, аргументи: памʼять<Ю8>): ц32 {
  ціль шлях = ю8"привіт.м";
  ціль значення_з_файлу = прочитати_файл(шлях);
  ціль текст_коду = виділити<ТекстКоду>();
  текст_коду.шлях = виділити_копію_ю8(шлях);
  текст_коду.значення = значення_з_файлу;
  ціль результат_розбору_мавки = розібрати_мавку(текст_коду);
  якщо результат_розбору_мавки.успіх == 0 {
    вивести_ю8(ю8"Помилка розбору: ");
    надрукувати_ю8(результат_розбору_мавки.помилка.повідомлення);
    вернути 1;
  }
  ціль М = МаМа::виділити_машину();
  ціль обʼєкт_дії_друк = МаМа::виділити_обʼєкт_дії(М);
  обʼєкт_дії_друк.виконати_ = виконати_друк;
  МаМа::змінити_в_середовищі(М.глобальне_середовище, МаМа::знайти_або_виділити_й_додати_назву(М.БН::адреса, ю8"друк"), обʼєкт_дії_друк);
  ціль код = МаМа::виділити_код();
  ціль контекст_компіляції = МаМа::виділити_контекст_компіляції();
  ціль результат_компіляції = МаМа::скомпілювати_тіло(М, контекст_компіляції, код, результат_розбору_мавки.тіло);
  МаМа::надрукувати_код(код);
  МаМа::виконати_код(М, код);
  вернути 0;
}